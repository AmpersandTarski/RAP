CONTEXT "AccountAndRoleMgt" IN ENGLISH
-- No support yet for Persona's.
-----------------------------------------------------------

VIEW Accounts: Account DEFAULT { "uid": accUserid } ENDVIEW

POPULATION Role CONTAINS [ "AccountMgr" ]

RELATION accNewPassword[Account*Password] [UNI]
ROLE ExecEngine MAINTAINS "(Re)set the password of an account"
RULE "(Re)set the password of an account": 
   accNewPassword /\ (sessionAccount~ \/ V;'AccountMgr';sessionActiveRoles~);"_SESSION";V |- -V
MEANING "(Re)setting the password for an account can only be done by an AccountMgr or the account user."
VIOLATION (TXT "{EX} InsPair;accPassword;Account;", SRC I, TXT ";Password;", TGT I
          ,TXT "{EX} DelPair;accNewPassword;Account;", SRC I, TXT ";Password;", TGT I
          )
RULE "(Re)setting the password for an account can only be done by an AccountMgr or the account user":
   accNewPassword |- (sessionAccount~ \/ V;'AccountMgr';sessionActiveRoles~);"_SESSION";V

INTERFACE "Accounts" FOR "AccountMgr": "_SESSION";V[SESSION*Account]  cRud BOX <SCOLS>
   [ "Userid": I cRUd LINKTO INTERFACE "Account"
   , "Person": accPerson cRUd
   , "Organization": accOrg cRUd
   , "Allowed roles": accAllowedRoles cRUd
   , "Default roles": accDefaultRoles cRUd
   , "Active?": accIsActive cRUd
   ]
           
INTERFACE "Account" FOR "AccountMgr": I[Account] cRUd ROWS
   [ "Userid": accUserid cRUd
   , "(Re)set password": accNewPassword crUd -- crUd is needed for Passwords
   , "Person": accPerson cRUd
   , "Organization": accOrg cRUd
   , "Allowed roles": accAllowedRoles cRUd
   , "Default roles": accDefaultRoles cRUd
   , "Active?": accIsActive cRUd
   ]

INTERFACE "My Account": "_SESSION";sessionAccount cRUd ROWS
   [ "Userid": accUserid cRUd
   , "New password": accNewPassword crUd -- crUd is needed for Passwords
   , "Person": accPerson cRud
   , "Organization": accOrg cRud
   , "Allowed roles": accAllowedRoles cRud
   , "Default roles": accDefaultRoles cRUd
   ]

--[Roles]--

INTERFACE "Roles" FOR "AccountMgr": "_SESSION";V[SESSION*Role] CRUd BOX <SCOLS>
   [ "Role": I cRud
   , "Assigned to": accAllowedRoles~ cRUd
   , "Default for": accDefaultRoles~ cRUd
   ]

ENDCONTEXT