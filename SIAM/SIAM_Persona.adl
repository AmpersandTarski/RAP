CONTEXT "Persona" IN ENGLISH
PURPOSE CONTEXT "Persona" 
{+ There is a subtle difference between Actors (that perform actions) and
Parties that are responsible for such actions (or better: accountable). 
We introduce the concept 'Persona', instances of which combine the two.
+}

--[Persona (minimalistic)]--
IDENT Persona: Persona(pActor,pParty,pRelation)

RELATION pActor[Persona*Actor] [UNI,TOT] MEANING "A Persona is associated with one Actor that performs all of its actions"
RELATION pParty[Persona*Party] [UNI,TOT] MEANING "A Persona is associated with one Party that is accountable for all of its actions"
RELATION pRelation[Persona*Relationship] [UNI] MEANING "A Persona is associated with a particular relationship (e.g. that of 'employee') that its Actor and Party have with one another."

RELATION actorPartyRelationship[Actor*Party]
MEANING "For (at least) one purpose, an Actor may act for a Party"

ROLE ExecEngine MAINTAINS "Actor-Party relationship integrity (1)"
RULE "Actor-Party relationship integrity (1)": pActor~;pParty |- actorPartyRelationship
VIOLATION (TXT "{EX} InsPair;actorPartyRelationship;Actor;", SRC I, TXT ";Party;", TGT I)
ROLE ExecEngine MAINTAINS "Actor-Party relationship integrity (2)"
RULE "Actor-Party relationship integrity (2)": actorPartyRelationship |- pActor~;pParty
VIOLATION (TXT "{EX} DelPair;actorPartyRelationship;Actor;", SRC I, TXT ";Party;", TGT I)

--[Accounts and Sessions]-- 
RELATION accPersona[Account*Persona]  [UNI]
MEANING "An Account may contain (web)service-specific attributes regarding a Persona"

RULE "Integrity of Persons, Organizations and Persona related to an Account":
   accPersona;accPersona~ /\ accOrg;accOrg~ /\ accPerson;accPerson~ 
|- accPersona;(pParty;accOrg~ /\ pActor;accPerson~)

ROLE ExecEngine MAINTAINS Create Persona from accOrg and accPerson"
RULE "Create Persona from accOrg and accPerson":
   accPerson~;accOrg |- pActor~;pParty
VIOLATION (TXT "{EX} NewStruct;Persona"
               ,TXT ";pActor;Persona;_NEW;Actor;", SRC I[Person]
               ,TXT ";pParty;Persona;_NEW;Organization;", TGT I[Organization]
          )     

ROLE ExecEngine MAINTAINS "Delete incomplete Persona"
RULE "Delete incomplete Persona": I[Persona] |- pActor;pActor~ /\ pParty;pParty~
VIOLATION (TXT "{EX} DelAtom;Persona;", SRC I[Persona])

ROLE ExecEngine MAINTAINS "Fill in accPerson"
RULE "Fill in accPerson":
   (I-(accPerson;accPerson~));accPersona;pActor |- accPerson
VIOLATION (TXT "{EX} InsPair;accPerson;Account;", SRC I, TXT ";Person;", TGT I[Person])

ROLE ExecEngine MAINTAINS "Fill in accOrganization"
RULE "Fill in accOrganization":
   (I-(accOrg;accOrg~));accPersona;pParty |- accOrg
VIOLATION (TXT "{EX} InsPair;accOrg;Account;", SRC I, TXT ";Organization;", TGT I[Organization])

ROLE ExecEngine MAINTAINS "Assign Persona to Account"
RULE "Assign Persona to Account": 
  (I[Account]-(accPersona;accPersona~));(accPerson;pActor~ /\ accOrg;pParty~) |- accPersona
VIOLATION (TXT "{EX} InsPair;accPersona;Account;", SRC I, TXT ";Persona;", TGT I)

RELATION sessionPersona[SESSION*Persona] [UNI]
MEANING "In a SESSION, the user (i.e. the Persona that acts 'at the other side') may be known."
ROLE ExecEngine MAINTAINS "Determine the acting persona in a session"
RULE "Determine the acting persona in a session": sessionAccount;accPersona |- sessionPersona
VIOLATION (TXT "{EX} InsPair;sessionPersona;SESSION;", SRC I, TXT ";Persona;", TGT I)
ROLE ExecEngine MAINTAINS "Undetermine the acting persona in a session"
RULE "Undetermine the acting persona in a session": sessionPersona |- sessionAccount;accPersona
VIOLATION (TXT "{EX} DelPair;sessionPersona;SESSION;", SRC I, TXT ";Persona;", TGT I)

--[Glossary]--
CONCEPT Persona "the combination of an Actor (that performs actions) and a Party (that is accountable for such acctions), within the context of their mutual relationship"

CONCEPT Actor "an entity that is capable of acting, such as a person, or a web-service"

CONCEPT Party "an entity that is capable of being accountable, such as an organization, or an individual person"

REPRESENT Relationship TYPE ALPHANUMERIC
CONCEPT Relationship "a (set of) purpose(s) that an Actor works to achieve and for which a Party is accountable"

-----------------------------------------------------------
ENDCONTEXT