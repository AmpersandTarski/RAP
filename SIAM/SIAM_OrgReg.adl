CONTEXT "Organization Registration" IN ENGLISH
{- This is a stand-alone file that implements an Organization registration is a minimalistic fashion.
Note that it does not contain an IDENT statement, so as to allow for easy extensions.
-}
-----------------------------------------------------------

CLASSIFY Organization ISA Party -- Integration with Persona

REPRESENT OrgAbbrName TYPE ALPHANUMERIC
RELATION orgAbbrName[Organization*OrgAbbrName] [INJ,UNI]
MEANING "An organization may have a short name by which it can be identified"

REPRESENT OrgFullName TYPE ALPHANUMERIC
RELATION orgFullName[Organization*OrgFullName] [INJ,UNI]
MEANING "An organization has a long (full) name by which it may be identified"

--[Accounts and Sessions]-- 
RELATION accOrg[Account*Organization] [UNI]
MEANING "An Account may contain (web)service-specific attributes regarding a specific Organization"

RELATION sessionOrg[SESSION*Organization] [UNI]
MEANING "In a session, the (accountable) Organization may be known."
ROLE ExecEngine MAINTAINS "Activation of session Organization"
RULE "Activation of session Organization": sessionAccount;accOrg |- sessionOrg
MEANING "The Organization of the user of the session may be known."
VIOLATION (TXT "{EX} InsPair;sessionOrg;SESSION;", SRC I, TXT ";Organization;", TGT I)
ROLE ExecEngine MAINTAINS "Deactivation of session Organization"
RULE "Deactivation of session Organization": sessionOrg |- sessionAccount;accOrg
MEANING "The Organization of the user of the session may be known."
VIOLATION (TXT "{EX} DelPair;sessionOrg;SESSION;", SRC I, TXT ";Organization;", TGT I)

--[Glossary]--
CONCEPT Organization "an organized group of people with a particular purpose, such as a business or government department" "Oxford English Dictionary"

-----------------------------------------------------------
ENDCONTEXT