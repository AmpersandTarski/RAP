CONTEXT "AutoLogin" IN ENGLISH
-- This file implements the auto-login feature, i.e. it defines the property `autoLoginAccount`.

RELATION autoLoginAccount[Account*Account] [PROP] -- An account that has this property is automatically logged in.

RULE "AutoLoginAccounts must be active": autoLoginAccount |- accIsActive

ROLE ExecEngine MAINTAINS "Automatically login"
RULE "Automatically login": autoLoginAccount /\ accIsActive /\ V;("_SESSION"-(sessionAccount;sessionAccount~));V |- -V
VIOLATION (TXT "{EX} InsPair;sessionAccount;SESSION;", SRC V;"_SESSION"[SESSION], TXT ";Account;", TGT I)

ROLE ExecEngine MAINTAINS "Disable automated login"
RULE "Disable automated login": autoLoginAccount /\ V;("_SESSION" /\ sessionAccount;sessionAccount~);V |- -V
VIOLATION (TXT "{EX} DelPair;autoLoginAccount;Account;", SRC I, TXT ";Account;", TGT I)

ENDCONTEXT