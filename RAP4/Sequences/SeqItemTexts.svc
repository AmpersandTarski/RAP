CONTEXT SequenceItemTextFormattingService IN ENGLISH MARKDOWN
PURPOSE CONTEXT SequenceItemTextFormattingService
{+The purpose of this service is to ensure that:
  * Every item that has an `itemSinValue` has a corresponding `itemSinText` 
    that is the concatentation of 
    1) a prefix text, as specified by `seqSinPrefix` of the sequence to which the Item belongs,
    2) the Item's `itemSinValue`, and
    3) a postfix text, as specified by `seqSinPostfix` of the sequence to which the Item belongs.
  * Every item that does not have an `itemSinValue`, has no `itemSinText`.
+}

--[Sequence Item  Texts]-----------------------------

RELATION itemSinText[Item*SinText] [UNI] REPRESENT SinText TYPE ALPHANUMERIC -- Concatenated texts
RELATION seqSinPrefix[Sequence*SinText] [UNI] -- Prefix to be used in item numbering texts
RELATION seqSinPostfix[Sequence*SinText] [UNI] -- Postfix to be used in item numbering texts

-- We need to remember the values that are used for creating the SinText to enable detection of any changes, and consequently the recomputation of SinTexts.
RELATION itemUsedPrefix[Item*SinText] [UNI] -- Prefix that was used for the item numbering texts
RELATION itemUsedValue[Item*SinValue] [UNI] -- the value of `itemSinValue` that was used to construct the corresponding `itemSinText`
RELATION itemUsedPostfix[Item*SinText] [UNI] -- Postfix that was used for the item numbering texts

{- Decision table for deleting an itemUsedPrefix or itemUserdPostfix:
   ----------------------------------------------------------------
   | itemUsedP[x]fix exists  |      yes      |   yes  |  no    no |
   | seqPre/Postfix exists   |      yes      |   no   | yes |  no |
   | compare seqPx, usedPx   |  eql |   neq  |   not applicable   |
   ----------------------------------------------------------------
   | Delete itemUsedP[x]fix? |  no  | yes[1] | yes[2] |  no |  no |
   ----------------------------------------------------------------
-}
-- The reason why the rules 'Auto-delete itemSinTexts' have distinct instances for the Prefixes, Postfixes and Sinvalues, is that Ampersand v3.2.0 [9f8f087] runs into memory problems if you OR these rules together.
ROLE ExecEngine MAINTAINS "Auto-delete itemSinTexts (Prefixes)" --
RULE "Auto-delete itemSinTexts (Prefixes)":
   (itemUsedPrefix;V  /\ itemInSeq;(I - seqSinPrefix;seqSinPrefix~)) -- yes[2] for 'Prefix'
   \/ (itemInSeq /\  itemUsedPrefix;-I;seqSinPrefix~)                -- yes[1] for 'Prefix'
   |- -V
VIOLATION ( TXT "{EX} DelPair;itemSinText;Item;",    SRC I, TXT ";SinText;", SRC itemSinText
          , TXT "{EX} DelPair;itemUsedPrefix;Item;", SRC I, TXT ";SinText;", SRC itemUsedPrefix
          , TXT "{EX} DelPair;itemUsedValue;Item;",  SRC I, TXT ";SinValue;", SRC itemUsedValue
          , TXT "{EX} DelPair;itemUsedPostfix;Item;",SRC I, TXT ";SinText;", SRC itemUsedPostfix
          )

ROLE ExecEngine MAINTAINS "Auto-delete itemSinTexts (Postfixes)" --
RULE "Auto-delete itemSinTexts (Postfixes)":
   (itemUsedPostfix;V /\ itemInSeq;(I-seqSinPostfix;seqSinPostfix~)) -- yes[2] for 'Postfix'
   \/ (itemInSeq /\ itemUsedPostfix;-I;seqSinPostfix~)               -- yes[1] for 'Postfix'
   |- -V
VIOLATION ( TXT "{EX} DelPair;itemSinText;Item;",    SRC I, TXT ";SinText;", SRC itemSinText
          , TXT "{EX} DelPair;itemUsedPrefix;Item;", SRC I, TXT ";SinText;", SRC itemUsedPrefix
          , TXT "{EX} DelPair;itemUsedValue;Item;",  SRC I, TXT ";SinValue;", SRC itemUsedValue
          , TXT "{EX} DelPair;itemUsedPostfix;Item;",SRC I, TXT ";SinText;", SRC itemUsedPostfix
          )

ROLE ExecEngine MAINTAINS "Auto-delete itemSinTexts (SinValues)"
RULE "Auto-delete itemSinTexts (SinValues)": 
   (itemUsedValue;V /\ (I - itemSinValue;itemSinValue~)) -- yes[2] for 'SinValue'
   \/ (I /\ itemSinValue;-I;itemUsedValue~)              -- yes[1] for 'SinValue'
   |- -V
VIOLATION ( TXT "{EX} DelPair;itemSinText;Item;",    SRC I, TXT ";SinText;", SRC itemSinText
          , TXT "{EX} DelPair;itemUsedPrefix;Item;", SRC I, TXT ";SinText;", SRC itemUsedPrefix
          , TXT "{EX} DelPair;itemUsedValue;Item;",  SRC I, TXT ";SinValue;", SRC itemUsedValue
          , TXT "{EX} DelPair;itemUsedPostfix;Item;",SRC I, TXT ";SinText;", SRC itemUsedPostfix
          )

{- Decision table for inserting or modifying ItemTexts:
   -------------------------------------------------------------------
   | seqP[x]fix exists       |              yes      |       no      |
   | itemUsedP[x]fix exists  |      yes      |   no  |      n|a      |
   | compare seqPx, usedPx   |  eql  |  neq  |     not applicable    |
   -------------------------------------------------------------------
   | Insert itemUsedP[x]fix? |   no  |  yes  |  yes  |       no      |
   -------------------------------------------------------------------
-}

ROLE ExecEngine MAINTAINS "Auto-create itemSinTexts upon SinValue changes"
RULE "Auto-create itemSinTexts upon SinValue changes":
   itemSinValue |- itemUsedValue
VIOLATION ( TXT "{EX} sinComputeItemText;itemSinText;Item;", SRC I
                 , TXT ";SinText;", SRC itemInSeq;seqSinPrefix
                 , TXT         ";", SRC itemSinValue 
                 , TXT         ";", SRC itemInSeq;seqSinPostfix
          , TXT "{EX} InsPair;itemUsedPrefix;Item;", SRC I, TXT ";SinText;", SRC itemInSeq;seqSinPrefix
          , TXT "{EX} InsPair;itemUsedValue;Item;",  SRC I, TXT ";SinValue;", SRC itemSinValue
          , TXT "{EX} InsPair;itemUsedPostfix;Item;",SRC I, TXT ";SinText;", SRC itemInSeq;seqSinPostfix
          )

ROLE ExecEngine MAINTAINS "Auto-create itemSinTexts upon seqSinPrefix changes"
RULE "Auto-create itemSinTexts upon seqSinPrefix changes":
   (I /\ itemSinValue;itemSinValue~);itemInSeq;seqSinPrefix |- itemUsedPrefix
VIOLATION ( TXT "{EX} sinComputeItemText;itemSinText;Item;", SRC I
                 , TXT ";SinText;", SRC itemInSeq;seqSinPrefix
                 , TXT         ";", SRC itemSinValue 
                 , TXT         ";", SRC itemInSeq;seqSinPostfix
          , TXT "{EX} InsPair;itemUsedPrefix;Item;", SRC I, TXT ";SinText;", SRC itemInSeq;seqSinPrefix
          , TXT "{EX} InsPair;itemUsedValue;Item;",  SRC I, TXT ";SinValue;", SRC itemSinValue
          , TXT "{EX} InsPair;itemUsedPostfix;Item;",SRC I, TXT ";SinText;", SRC itemInSeq;seqSinPostfix
          )

ROLE ExecEngine MAINTAINS "Auto-create itemSinTexts upon seqSinPostfix changes"
RULE "Auto-create itemSinTexts upon seqSinPostfix changes":
   (I /\ itemSinValue;itemSinValue~);itemInSeq;seqSinPostfix |- itemUsedPostfix
VIOLATION ( TXT "{EX} sinComputeItemText;itemSinText;Item;", SRC I
                 , TXT ";SinText;", SRC itemInSeq;seqSinPrefix
                 , TXT         ";", SRC itemSinValue 
                 , TXT         ";", SRC itemInSeq;seqSinPostfix
          , TXT "{EX} InsPair;itemUsedPrefix;Item;", SRC I, TXT ";SinText;", SRC itemInSeq;seqSinPrefix
          , TXT "{EX} InsPair;itemUsedValue;Item;",  SRC I, TXT ";SinValue;", SRC itemSinValue
          , TXT "{EX} InsPair;itemUsedPostfix;Item;",SRC I, TXT ";SinText;", SRC itemInSeq;seqSinPostfix
          )

ENDCONTEXT