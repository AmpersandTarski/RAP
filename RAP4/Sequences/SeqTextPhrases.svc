CONTEXT SequenceTextConstructionService IN ENGLISH
{- Under the assumption that all Items in a Sequence have a phrase associated with them,
this service computes the concatentation of all such phrases. 
The result is that `seqLastItem;itemBigText` is the concatenation of all phrases of the items of the sequence.

Note: the database automatically strips leading and trailing whitespace from texts (phrases).
The constructor therefor automatically inserts a (single) space between two concatenated texts.
-}

REPRESENT BigText TYPE BIGALPHANUMERIC
itemPhrase :: Item * BigText [UNI] 
itemBigText :: Item * BigText [UNI] 

ROLE ExecEngine MAINTAINS SetBigTextFirst
RULE SetBigTextFirst LABEL "Set BigText of first item in sequence": 
   seqFirstItem;itemPhrase |- seqFirstItem;itemBigText
VIOLATION (TXT "{EX} InsPair;itemBigText;Item;", SRC seqFirstItem, TXT ";BigText;", TGT I)

ROLE ExecEngine MAINTAINS ClearBigTextFirst
RULE ClearBigTextFirst LABEL "Clear BigText of first item in sequence": 
   seqFirstItem;itemBigText |- seqFirstItem;itemPhrase
VIOLATION (TXT "{EX} DelPair;itemBigText;Item;", SRC seqFirstItem, TXT ";BigText;", TGT I)

ROLE ExecEngine MAINTAINS SetBigTextNext
RULE SetBigTextNext LABEL "Set BigText of next items in sequence": 
   (nextItem~;itemBigText;tcPrefix~ /\ itemPhrase;tcLabel~);tcResult |- itemBigText
VIOLATION (TXT "{EX} InsPair;itemBigText;Item;", SRC I, TXT ";BigText;", TGT I)

IDENT TextConstructs: TextConstruct(tcPrefix,tcLabel)
tcPrefix :: TextConstruct * BigText [UNI]
tcLabel  :: TextConstruct * BigText [UNI]
tcResult :: TextConstruct * BigText [UNI]

ROLE ExecEngine MAINTAINS CreateTextConstructs
RULE CreateTextConstructs LABEL "Create TextConstructs": itemBigText~;nextItem;itemPhrase |- tcPrefix~;tcLabel
VIOLATION ( TXT "{EX} InsAtom;TextConstruct"
          , TXT "{EX} InsPair;tcPrefix;TextConstruct;_NEW;BigText;", SRC I
          , TXT "{EX} InsPair;tcLabel;TextConstruct;_NEW;BigText;", TGT I
-- The line below may make the rule 'Compute TextConstructs' obsolete.
--        , TXT "{EX} InsPair;tcResult;TextConstruct;_NEW;BigText;{php}implode('',array(", SRC I, TXT ",", TGT I, TXT "))"
          )

ROLE ExecEngine MAINTAINS ComputeTextConstructs
RULE ComputeTextConstructs LABEL "Compute TextConstructs": I /\ tcPrefix;tcPrefix~ /\ tcLabel;tcLabel~ |- tcResult;tcResult~
VIOLATION (TXT "{EX} ConstrucText;tcResult;TextConstruct;", SRC I, TXT ";BigText;", SRC tcPrefix, TXT ";", SRC tcLabel)

ROLE ExecEngine MAINTAINS DeleteTextConstructs
RULE DeleteTextConstructs LABEL "Delete TextConstructs": I |- tcPrefix;itemBigText~;nextItem;itemPhrase;tcLabel~
VIOLATION (TXT "{EX} DelAtom;TextConstruct;", SRC I)

ENDCONTEXT