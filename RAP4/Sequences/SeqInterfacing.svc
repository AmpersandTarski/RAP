CONTEXT SequenceInterfacingServices IN ENGLISH MARKDOWN
PURPOSE CONTEXT SequenceInterfacingServices
{+This CONTEXT contains a number of services that are useful in interfacing. They include:
- Remove an existing item from a sequence (but not delete it);
- Delete an existing item (either an orphaned item or a sequenced item);
- Insert an existing item into a sequence, either before or after a specified item;
- Create a new item and insert it at the beginning or end of a sequence;
- Create a new item and insert it before or after a specified item in a sequence.
+}

--[Insert an existing 'next item' for an item in a sequence]--
PURPOSE RELATION itemInsNextItem
{+`itemInsNextItem` spefies an Item (TGT) that is to be inserted after/behind a specified (SRC) Item+}
RELATION itemInsNextItem[Item*Item] [UNI] -- TGT Item must be inserted as the next item of the SRC Item

RULE "SRC Items in `itemInsNextItem` must be in a Sequence": itemInsNextItem |- itemInSeq;V
RULE "TGT Items in `itemInsNextItem` must be orphans": itemInsNextItem |- V;(I-(itemInSeq;itemInSeq~))

ROLE ExecEngine MAINTAINS "Insert an existing 'next item' for an item in a sequence"
RULE "Insert an existing 'next item' for an item in a sequence":
   (I /\ itemInSeq;itemInSeq~);itemInsNextItem;(I-(itemInSeq;itemInSeq~))
|- nextItem /\ itemInSeq;itemInSeq~
VIOLATION ( TXT "{EX} InsPair;itemInSeq;Item;", TGT I, TXT ";Sequence;", SRC itemInSeq
          , TXT "{EX} InsPair;nextItem;Item;", TGT I, TXT ";Item;", SRC nextItem
          , TXT "{EX} InsPair;nextItem;Item;", SRC I, TXT ";Item;", TGT I
          , TXT "{EX} DelPair;itemInsNextItem;Item;", SRC I, TXT ";Item;", TGT I
          )

--[Insert an existing 'previous item' for an item in a sequence]--
PURPOSE RELATION itemInsPrevItem
{+`itemInsPrevItem` specifies an Item (TGT) that is to be inserted before a specified (SRC) Item+}
RELATION itemInsPrevItem[Item*Item] [UNI] -- TGT Item must be inserted as the previous item of the SRC Item

RULE "SRC Items in `itemInsPrevItem` must be in a Sequence": itemInsPrevItem |- itemInSeq;V
RULE "TGT Items in `itemInsPrevItem` must be orphans": itemInsPrevItem |- V;(I-(itemInSeq;itemInSeq~))

ROLE ExecEngine MAINTAINS "Insert an existing item as the 'previous item' for a non-first item of a sequence"
RULE "Insert an existing item as the 'previous item' for a non-first item of a sequence":
   (I /\ nextItem~;itemInSeq;itemInSeq~);itemInsPrevItem;(I-(itemInSeq;itemInSeq~))
|- nextItem~ /\ itemInSeq;itemInSeq~
VIOLATION ( TXT "{EX} InsPair;itemInSeq;Item;", TGT I, TXT ";Sequence;", SRC itemInSeq
          , TXT "{EX} InsPair;nextItem;Item;", TGT I, TXT ";Item;", SRC I
          , TXT "{EX} InsPair;nextItem;Item;", SRC nextItem~, TXT ";Item;", TGT I
          , TXT "{EX} DelPair;itemInsPrevItem;Item;", SRC I, TXT ";Item;", TGT I
          )

ROLE ExecEngine MAINTAINS "Insert an existing item as the 'previous item' for a first item of a sequence"
RULE "Insert an existing item as the 'previous item' for a first item of a sequence":
   (I /\ itemInSeq;seqFirstItem);itemInsPrevItem;(I-(itemInSeq;itemInSeq~))
|- nextItem~ /\ itemInSeq;itemInSeq~
VIOLATION ( TXT "{EX} InsPair;itemInSeq;Item;", TGT I, TXT ";Sequence;", SRC itemInSeq
          , TXT "{EX} InsPair;nextItem;Item;", TGT I, TXT ";Item;", SRC I
          , TXT "{EX} DelPair;itemInsPrevItem;Item;", SRC I, TXT ";Item;", TGT I
          )

--[Removing Items]--
PURPOSE RELATION itemRmvReq
{+`itemRmvReq` is a property of an Item that, when set, requests the Item to be removed from its Sequence+}
RELATION itemRmvReq[Item*Item] [PROP] -- Request that the Item is removed from the Sequence (but not deleted)

RULE "`itemRmvReq` may only be populated for Items that are in a Sequence": itemRmvReq |- itemInSeq;V

ROLE ExecEngine MAINTAINS "Removing a non-first item of a sequence"
RULE "Removing a non-first item of a sequence":
   itemRmvReq;nextItem~;itemInSeq |- -itemInSeq
VIOLATION ( TXT "{EX} InsPair;nextItem;Item;", SRC nextItem~, TXT ";Item;", SRC nextItem -- Update link in Sequence
          , TXT "{EX} DelPair;nextItem;Item;", SRC I, TXT ";Item;", SRC nextItem -- Remove nextItem link from orphaned Item
          , TXT "{EX} DelPair;itemInSeq;Item;", SRC I, TXT ";Sequence;", TGT I -- Remove orphaned Item from Sequence
          , TXT "{EX} DelPair;itemRmvReq;Item;", SRC I, TXT ";Item;", SRC I -- Reset `itemRmvReq`
          )

ROLE ExecEngine MAINTAINS "Removing the first item of a sequence"
RULE "Removing the first item of a sequence":
  itemRmvReq;seqFirstItem~ |- -itemInSeq
VIOLATION ( TXT "{EX} DelPair;nextItem;Item;", SRC I, TXT ";Item;", SRC nextItem -- Remove nextItem link from orphaned Item
          , TXT "{EX} DelPair;itemInSeq;Item;", SRC I, TXT ";Sequence;", TGT I -- Remove orphaned Item from Sequence
          , TXT "{EX} DelPair;itemRmvReq;Item;", SRC I, TXT ";Item;", SRC I -- Reset `itemRmvReq`
          )

--[Creating new Items]--
PURPOSE RELATION seqNewFirstItem
{+`seqNewFirstItem` spefies the name of a Item that is to be created and inserted as a new first item in a Sequence+}
RELATION seqNewFirstItem[Sequence*ItemName] [UNI] -- Create a new Item called 'ItemName', inserting it as the first Item

ROLE ExecEngine MAINTAINS "Create a 'new first item' in a non-empty sequence"
RULE "Create a 'new first item' in a non-empty sequence":
   (I /\ itemInSeq~;itemInSeq);seqNewFirstItem |- seqFirstItem;itemName
VIOLATION ( TXT "{EX} InsAtom;Item"
          , TXT "{EX} InsPair;itemInSeq;Item;_NEW;Sequence;", SRC I
          , TXT "{EX} InsPair;itemName;Item;_NEW;ItemName;", TGT I
          , TXT "{EX} InsPair;nextItem;Item;_NEW;Item;", SRC seqFirstItem
          , TXT "{EX} DelPair;seqNewFirstItem;Sequence;", SRC I, TXT ";ItemName;", TGT I
          )

ROLE ExecEngine MAINTAINS "Create a 'new first item' in an empty sequence"
RULE "Create a 'new first item' in an empty sequence": 
   (I-(itemInSeq~;itemInSeq));seqNewFirstItem |- seqFirstItem;itemName
VIOLATION ( TXT "{EX} InsAtom;Item"
          , TXT "{EX} InsPair;itemInSeq;Item;_NEW;Sequence;", SRC I
          , TXT "{EX} InsPair;itemName;Item;_NEW;ItemName;", TGT I
          , TXT "{EX} DelPair;seqNewFirstItem;Sequence;", SRC I, TXT ";ItemName;", TGT I
          )

--[Create new Last Item]--
PURPOSE RELATION seqNewLastItem
{+`seqNewLastItem` spefies the name of a Item that is to be created and inserted as a new Last item in a Sequence+}
RELATION seqNewLastItem[Sequence*ItemName] [UNI] -- Create a new Item called 'ItemName', inserting it as the last Item

ROLE ExecEngine MAINTAINS "Create a 'new last item' in a non-empty sequence"
RULE "Create a 'new last item' in a non-empty sequence":
   (I /\ itemInSeq~;itemInSeq);seqNewLastItem |- seqLastItem;itemName
VIOLATION ( TXT "{EX} InsAtom;Item"
          , TXT "{EX} InsPair;itemInSeq;Item;_NEW;Sequence;", SRC I
          , TXT "{EX} InsPair;itemName;Item;_NEW;ItemName;", TGT I
          , TXT "{EX} InsPair;nextItem;Item;", SRC seqLastItem, TXT ";Item;_NEW"
          , TXT "{EX} DelPair;seqNewLastItem;Sequence;", SRC I, TXT ";ItemName;", TGT I
          )

ROLE ExecEngine MAINTAINS "Create a 'new last item' in an empty sequence"
RULE "Create a 'new last item' in an empty sequence": 
   (I-(itemInSeq~;itemInSeq));seqNewLastItem |- seqLastItem;itemName
VIOLATION ( TXT "{EX} InsAtom;Item"
          , TXT "{EX} InsPair;itemInSeq;Item;_NEW;Sequence;", SRC I
          , TXT "{EX} InsPair;itemName;Item;_NEW;ItemName;", TGT I
          , TXT "{EX} DelPair;seqNewLastItem;Sequence;", SRC I, TXT ";ItemName;", TGT I
          )

--[Create a 'new next item' for an item in a sequence]--
PURPOSE RELATION itemNewNextItem
{+`itemNewNextItem` spefies the name of a Item that is to be created and inserted after the specified Item+}
RELATION itemNewNextItem[Item*ItemName] [UNI] -- Create a new Item called 'ItemName', inserting it after the specified Item

RULE "`itemNewNextItem` may only be populated for Items that are in a Sequence": itemNewNextItem |- itemInSeq;V

ROLE ExecEngine MAINTAINS "Create a 'new next item' for an item in a sequence"
RULE "Create a 'new next item' for an item in a sequence":
   (I /\ itemInSeq;itemInSeq~);itemNewNextItem |- (nextItem /\ itemInSeq;itemInSeq~);itemName
VIOLATION ( TXT "{EX} InsAtom;Item"
          , TXT "{EX} InsPair;itemInSeq;Item;_NEW;Sequence;", SRC itemInSeq
          , TXT "{EX} InsPair;itemName;Item;_NEW;ItemName;", TGT I
          , TXT "{EX} InsPair;nextItem;Item;_NEW;Item;", SRC nextItem
          , TXT "{EX} InsPair;nextItem;Item;", SRC I, TXT ";Item;_NEW"
          , TXT "{EX} DelPair;itemNewNextItem;Item;", SRC I, TXT ";ItemName;", TGT I
          )

--[Create a 'new previous item' for an item in a sequence]--
PURPOSE RELATION itemNewPrevItem
{+`itemNewPrevItem` spefies the name of a Item that is to be created and inserted prior to the specified Item+}
RELATION itemNewPrevItem[Item*ItemName] [UNI] -- Create a new Item called 'ItemName', inserting it before the specified Item

RULE "`itemNewPrevItem` may only be populated for Items that are in a Sequence": itemNewPrevItem |- itemInSeq;V

ROLE ExecEngine MAINTAINS "Create a 'new previous item' for a non-first item of a sequence"
RULE "Create a 'new previous item' for a non-first item of a sequence":
   (I /\ nextItem~;itemInSeq;itemInSeq~);itemNewPrevItem |- (nextItem~ /\ itemInSeq;itemInSeq~);itemName
VIOLATION ( TXT "{EX} InsAtom;Item"
          , TXT "{EX} InsPair;itemInSeq;Item;_NEW;Sequence;", SRC itemInSeq
          , TXT "{EX} InsPair;itemName;Item;_NEW;ItemName;", TGT I
          , TXT "{EX} InsPair;nextItem;Item;_NEW;Item;", SRC I
          , TXT "{EX} InsPair;nextItem;Item;", SRC nextItem~, TXT ";Item;_NEW"
          , TXT "{EX} DelPair;itemNewPrevItem;Item;", SRC I, TXT ";ItemName;", TGT I
          )

ROLE ExecEngine MAINTAINS "Create a 'new previous item' for the first item of a sequence"
RULE "Create a 'new previous item' for the first item of a sequence":
   (I /\ itemInSeq;seqFirstItem);itemNewPrevItem |- (nextItem~ /\ itemInSeq;seqFirstItem);itemName
VIOLATION ( TXT "{EX} InsAtom;Item"
          , TXT "{EX} InsPair;itemInSeq;Item;_NEW;Sequence;", SRC itemInSeq
          , TXT "{EX} InsPair;itemName;Item;_NEW;ItemName;", TGT I
          , TXT "{EX} InsPair;nextItem;Item;_NEW;Item;", SRC I
          , TXT "{EX} DelPair;itemNewPrevItem;Item;", SRC I, TXT ";ItemName;", TGT I
          )

--[Deleting Items]--
PURPOSE RELATION itemDelReq
{+`itemDelReq` is a property of an Item that, when set, requests the Item to be Deleted (and hence also removed from its Sequence+}
RELATION itemDelReq[Item*Item] [PROP] -- Request that the Item is removed from the Sequence, and deleted

ROLE ExecEngine MAINTAINS "Deleting a non-first item of a sequence"
RULE "Deleting a non-first item of a sequence":
   I /\ nextItem~;itemInSeq;itemInSeq~ |- -itemDelReq
VIOLATION ( TXT "{EX} InsPair;nextItem;Item;", SRC nextItem~, TXT ";Item;", SRC nextItem
          , TXT "{EX} DelAtom;Item;", SRC I[Item]  -- Reset `itemDelReq` is  not necessary
          )

ROLE ExecEngine MAINTAINS "Deleting first items of a sequence and orphaned items"
RULE "Deleting first items of a sequence and orphaned items":
  I /\ (itemInSeq;seqFirstItem \/ -(itemInSeq;itemInSeq~)) |- -itemDelReq
VIOLATION (TXT "{EX} DelAtom;Item;", SRC I[Item])  -- Reset `itemDelReq` is  not necessary

ENDCONTEXT