CONTEXT AutoLogin IN ENGLISH
-- This file implements the auto-login feature, i.e. it defines the property `accAutoLoginReq`.

RELATION accAutoLoginReq[Account*Account] [PROP] -- An account that has this property is automatically logged in.

ROLE ExecEngine MAINTAINS ResetAutoLoginReq
RULE ResetAutoLoginReq LABEL "Reset `accAutoLoginReq` when account is inactive": accAutoLoginReq |- accIsActive
VIOLATION (TXT "{EX} DelPair;accAutoLoginReq;Account;", SRC I, TXT ";Account;", TGT I)

ROLE ExecEngine MAINTAINS ResetAutoLogin
RULE ResetAutoLogin LABEL "Reset `accAutoLoginReq` when a user has already logged in": accAutoLoginReq |- V;"_SESSION";sessionIsAnon;V
VIOLATION (TXT "{EX} DelPair;accAutoLoginReq;Account;", SRC I, TXT ";Account;", TGT I)

ROLE ExecEngine MAINTAINS AutoLogin -- When multiple accounts have this property, only one will be selected.
RULE AutoLogin: sessionIsAnon;V;(accAutoLoginReq /\ accIsActive) |- sessionIsUser;sessionAccount
VIOLATION ( TXT "{EX} InsPair;sessionAccount;SESSION;", SRC I, TXT ";Account;", TGT I
          , TXT "{EX} DelPair;accAutoLoginReq;Account;", TGT I, TXT ";Account;", TGT I
          , TXT "{EX} InsPair;sessionIsUser;SESSION;", SRC I, TXT ";SESSION;", SRC I
          , TXT "{EX} DelPair;sessionIsAnon;SESSION;", SRC I, TXT ";SESSION;", SRC I
          , TXT "{EX} SetNavToOnCommit;", SRC sessionUserIfc -- Setup for default user interface
          , TXT "{EX} SetNavToOnCommit;", TGT accUserIfc -- Override if Account has a specific setting
          )

ENDCONTEXT