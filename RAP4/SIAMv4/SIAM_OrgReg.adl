CONTEXT "Organization Registration" IN ENGLISH
{- This is a stand-alone file that implements an Organization registration is a relatively minimalistic fashion.
The purpose of this file is to provide a set of 'standard' relations that are commonly re-used in different projects.
Note that it does not contain an IDENT statement, so as to allow for easy extensions.
Also note that relations are UNI, not TOT - this is to allow as much flexibility for re-use as possible.
-}
-----------------------------------------------------------
INCLUDE "./SIAM_OrgRefs.adl"          -- a standard way for refering to people, not associated to the Person concept.

--CLASSIFY Organization ISA Party -- Integration with Persona

RELATION orgRef[Organization*OrgRef] [UNI] -- a name that may be used to refer to the Organization.
MEANING "A textstring to refer to this Organization"

CLASSIFY OrgAbbrName ISA OrgRef
REPRESENT OrgAbbrName TYPE ALPHANUMERIC
RELATION orgAbbrName[Organization*OrgAbbrName] [INJ,UNI,TOT]
MEANING "An organization has a short name by which it can be identified"

REPRESENT OrgFullName TYPE ALPHANUMERIC
RELATION orgFullName[Organization*OrgFullName] [INJ,UNI]
MEANING "An organization has a long (full) name by which it may be identified"

--[Accounts and Sessions]-- 
accOrg :: Account * Organization [UNI]
MEANING "An Account may contain (web)service-specific attributes regarding a specific Organization"

sessionOrg :: SESSION * Organization [UNI]
MEANING "In a session, the (accountable) Organization may be known."
ROLE ExecEngine MAINTAINS "Activation of session Organization"
RULE "Activation of session Organization": sessionAccount;accOrg |- sessionOrg
VIOLATION (TXT "{EX} InsPair;sessionOrg;SESSION;", SRC I, TXT ";Organization;", TGT I)
ROLE ExecEngine MAINTAINS "Deactivation of session Organization"
RULE "Deactivation of session Organization": sessionOrg |- sessionAccount;accOrg
VIOLATION (TXT "{EX} DelPair;sessionOrg;SESSION;", SRC I, TXT ";Organization;", TGT I)

--[Integration with SIAM_OrgRefs]--

ROLE ExecEngine MAINTAINS "Auto maintain 'orgRef' relation" -- Compute population of `orgRef`
RULE "Auto maintain 'orgRef' relation": orgAbbrName |- orgRef
VIOLATION (TXT "{EX} InsPair;orgRef;Organization;", SRC I, TXT ";OrgRef;", TGT I[OrgRef])

ROLE ExecEngine MAINTAINS "Auto maintain 'accOrgRef' relation"  -- Compute population of `accOrgRef`
RULE "Auto maintain 'accOrgRef' relation": accOrg;orgAbbrName |- accOrgRef
VIOLATION (TXT "{EX} InsPair;accOrgRef;Account;", SRC I, TXT ";OrgRef;", TGT I[OrgRef])

--[Glossary]--
-- NOTE: In GRC, the destinction between Organization and BusinessFunction is made, where Organizations are BusinessFunctions, and Organizations are accountable/governing and BusinessFunctions are responsible/managing.
CONCEPT Organization "an organized group of people with a particular purpose, such as a business or government department" "Oxford English Dictionary"

-----------------------------------------------------------
ENDCONTEXT