CONTEXT RAP4 IN ENGLISH

   INCLUDE "SIAMv4_importer.adl"
   INCLUDE "../Sequences/SeqOntology.adl"       -- Basic notions

   INCLUDE "Scripts.adl"
   INCLUDE "Scripts.docadl"
   INCLUDE "Scripts.ifc"

   INCLUDE "Analyse.ifc"

   INCLUDE "Contexts.ifc"
   INCLUDE "RAP4.ifc"

   INCLUDE "RAP4_AccountMgt.ifc"            -- RAP4 Account management interface
   INCLUDE "RAP4.xlsx"                      -- Initial, minimal set of people and accounts 

   INCLUDE "Atlas.ifc"                      -- Interfaces to export Altas population
   INCLUDE "Atlas.adl"                      -- rules and interfaces to guide the ATLAS editor
   

REPRESENT Language TYPE ALPHANUMERIC
REPRESENT AmpersandVersion,TType TYPE ALPHANUMERIC
REPRESENT Message TYPE BIGALPHANUMERIC
REPRESENT Population TYPE BIGALPHANUMERIC
REPRESENT ShowADL TYPE BIGALPHANUMERIC

RELATION compileresponse[Script*CompileResponse] [UNI] -- the most recent stdout message of compiling a script
RELATION compileresponse[ScriptVersion*CompileResponse] [UNI]
REPRESENT CompileResponse TYPE BIGALPHANUMERIC

CLASSIFY Script ISA Sequence
CLASSIFY ScriptVersion ISA Item
CLASSIFY Interface ISA ObjectDef
CONCEPT ObjectDef "An ObjectDef is the (recursive) structure that corresponds with a BOX in an Ampersand script." 
CLASSIFY ObjectDef ISA Term

--[[Manipulating Scripts with Ampersand]]--
PATTERN Compilation
--[Checking scripts]--
RELATION scriptOk[ScriptVersion*ScriptVersion] [PROP] -- A ScriptVersion may have the property that Ampersand can compile it

RELATION compileRequest[Script*Script][PROP]
ROLE ExecEngine MAINTAINS Checkscript

--RULE Checkscript LABEL "Check script" : compileRequest |- -V -- Zie comment 20161111/RJ hiervoor
RULE Checkscript LABEL "Check script" : compileRequest;submittor;accUserid |- -V[Script*Userid]
VIOLATION ( TXT "{EX}CompileToNewVersion;", SRC I,TXT ";", TGT I
          , TXT "{EX}DelPair;compileRequest;Script;", SRC I, TXT ";Script;", SRC I
          )

--[Functional specifiations]--
RELATION funcSpecRequest[ScriptVersion*ScriptVersion] [PROP] -- User has requested the script to be converted into a functional specification
RELATION funcSpecOk[ScriptVersion*ScriptVersion] [PROP] -- a functional specification has actually been generated (no errors)
RELATION funcSpec[ScriptVersion*FileObject] [UNI] -- the file **containing** the funcspec of the script, it has the property 'funcSpecOk'

ROLE ExecEngine MAINTAINS Createfspec -- reset funcSpecRequest after an attempt to create it has been performed.
--RULE Createfspec LABEL "Create fspec" : funcSpecRequest-funcSpec;funcSpec~ |- -V -- Zie comment 20161111/RJ hiervoor
RULE Createfspec LABEL "Create fspec" : (funcSpecRequest-funcSpec;funcSpec~);source;filePath |- -V[ScriptVersion*FilePath]
VIOLATION ( TXT "{EX}CompileWithAmpersand;fspec;", SRC version~, TXT ";", SRC I, TXT ";", TGT I, TXT ";", SRC version~;submittor;accUserid
          , TXT "{EX}DelPair;funcSpecRequest;ScriptVersion;", SRC I, TXT ";ScriptVersion;", SRC I
          )

--[Diagnosis]--
RELATION diagRequest[ScriptVersion*ScriptVersion] [PROP] -- User has requested the script to be converted into a diagnosis
RELATION diagOk[ScriptVersion*ScriptVersion] [PROP] -- a diagnosis has actually been generated (no errors)
RELATION diag[ScriptVersion*FileObject] [UNI] -- the file containing the diagnosis of the script, it has the property 'diagOk'

ROLE ExecEngine MAINTAINS Creatediagnosis -- reset diagRequest after an attempt to create it has been performed.
--RULE Creatediagnosis LABEL "Create diagnosis" : diagRequest-diag;diag~ |- -V -- Zie comment 20161111/RJ hiervoor
RULE Creatediagnosis LABEL "Create diagnosis" : (diagRequest-diag;diag~);source;filePath |- -V
VIOLATION ( TXT "{EX}CompileWithAmpersand;diagnosis;", SRC version~, TXT ";", SRC I, TXT ";",TGT I, TXT ";", SRC version~;submittor;accUserid
          , TXT "{EX}DelPair;diagRequest;ScriptVersion;", SRC I, TXT ";ScriptVersion;", SRC I
          )

--[Loading of the script]--
RELATION loadRequest[ScriptVersion*ScriptVersion] [PROP]  -- User has requested the script to be loaded into the FormalAmpersand metatables of RAP4
RELATION loadedInRAP4Ok[ScriptVersion*ScriptVersion] [PROP] -- The exec-engine has loaded the population of the script.
RELATION context[ScriptVersion*Context][UNI] -- The context that is created in RAP4 when it is loaded into the meta tables

ROLE ExecEngine MAINTAINS Loadpopulationintometatables -- reset loadRequest after an attempt to create it has been performed.
--RULE Loadpopulationintometatables LABEL "Load population into meta tables" : loadRequest-loadedInRAP4Ok |- -V -- Zie comment 20161111/RJ hiervoor
-- Also make sure only to populate a new atlas, if it will be the only atlas population for that user. (that will be taken care of by the rule named "Trash all previous atlases of this account".)
RULE Loadpopulationintometatables LABEL "Load population into meta tables" : ((loadRequest-loadedInRAP4Ok)-(version~;submittor;submittor~;version;context;V));source;filePath |- -V
VIOLATION ( TXT "{EX}CompileWithAmpersand;makeAtlas;", SRC version~, TXT ";", SRC I, TXT ";",TGT I, TXT ";", SRC version~;submittor;accUserid
          , TXT "{EX}DelPair;loadRequest;ScriptVersion;", SRC I, TXT ";ScriptVersion;", SRC I
          )
--[Cleaning up old atlases]
ROLE ExecEngine MAINTAINS Trashallpreviousatlasesofthisaccount -- before a new atlas is populated, make sure all previous ones are removed to prevent disorder.
RULE Trashallpreviousatlasesofthisaccount LABEL "Trash all previous atlases of this account" : loadRequest; version~ ;submittor;submittor~;version;context |- context;requestDestroy
VIOLATION (TXT "{EX}InsPair;requestDestroy;Context;", TGT I, TXT ";Context;", TGT I)

--[Prototypes]--
RELATION protoRequest[ScriptVersion*ScriptVersion] [PROP] -- User has requested the script to be converted into a prototype
RELATION protoOk[ScriptVersion*ScriptVersion] [PROP] -- a prototype has actually been generated (no errors)
RELATION proto[Script*FileObject] [UNI] -- the index.php (or whatever) file that must be clicked to start the prototype

ROLE ExecEngine MAINTAINS Createprototype -- reset protoRequest after an attempt to create it has been performed.
--RULE Createprototype LABEL "Create prototype" : protoRequest-proto;proto~ |- -V -- Zie comment 20161111/RJ hiervoor
RULE Createprototype LABEL "Create prototype" : (protoRequest-protoOk;protoOk~);source;filePath |- -V
VIOLATION ( TXT "{EX}CompileWithAmpersand;prototype;", SRC version~, TXT ";", SRC I, TXT ";",TGT I, TXT ";", SRC version~;submittor;accUserid
          , TXT "{EX}DelPair;protoRequest;ScriptVersion;", SRC I, TXT ";ScriptVersion;", SRC I
          )

--[Deletion of Context (Fully cascading!)]--
RELATION requestDestroy[Context*Context] [PROP] -- a request to destroy this context. Beware: Experimental. You might burn all. The idea is that ALL atoms in the context of RAP4 that are linked with this context atom (transitive closure) will be deleted. ALL relations are taken into account, except for a set, specified in cli.php. Currently, the only relation in that set is context[ScriptVersion*Context]. This is to avoid that atoms are destroyed in the specific RAP4 part. 
ROLE ExecEngine MAINTAINS DestroyContext  -- ExecEngine is een echte pyrotechnicus.... 
RULE DestroyContext LABEL "Destroy Context" : requestDestroy |- -V
VIOLATION ( TXT "{EX}Cleanup;", SRC I, TXT ";Context"
          )


--[[Interactive ATLAS -MO-]]--
-- dit wordt gebruikt om de ATLAS --> .ADL file te genereren
-- RELATION convertToADL[Script*Script][PROP]
-- ROLE ExecEngine MAINTAINS Convertadl

--[ATLAS converter]--
RELATION adlRequest[ScriptVersion*ScriptVersion] [PROP] -- User has requested the script to be converted into a functional specification
RELATION adlOk[ScriptVersion*ScriptVersion] [PROP] -- a functional specification has actually been generated (no errors)
RELATION adl[ScriptVersion*FileObject] [UNI] -- the file **containing** the funcspec of the script, it has the property 'funcSpecOk'

ROLE ExecEngine MAINTAINS Convertadl -- reset funcSpecRequest after an attempt to create it has been performed.
--RULE Createfspec LABEL "Create fspec" : funcSpecRequest-funcSpec;funcSpec~ |- -V -- Zie comment 20161111/RJ hiervoor
RULE Convertadl LABEL "Convert adl" : (adlRequest-adl;adl~);source;filePath |- -V[ScriptVersion*FilePath]
VIOLATION ( TXT "{EX}ConvertToADL;", SRC version~, TXT ";", SRC I, TXT ";", TGT I, TXT ";", SRC version~;submittor;accUserid
          , TXT "{EX}DelPair;adlRequest;ScriptVersion;", SRC I, TXT ";ScriptVersion;", SRC I
          )



ENDPATTERN

PATTERN Runtime
    RELATION atom[InsAtom*Atom] -- 
    RELATION content[Div*Atom] -- 
    RELATION div[InsAtom * Div]
    RELATION field[Div * Field]
    RELATION def[Field * FieldDef]
    RELATION def[Object * ObjectDef]
    RELATION fieldIn[Field * Object]
    RELATION eval[Term * Pair]
    RELATION in[Pair*Relation]
ENDPATTERN

PATTERN PairViews LABEL "Pair views"
    RELATION expSQL[PairViewSegment * MySQLQuery] [UNI]
    RELATION expTgt[PairViewSegment * Concept]
    RELATION segment[PairView * PairViewSegment] [INJ]
    RELATION segmentType[PairViewSegment * PairViewSegmentType] [UNI]
    RELATION sequenceNr[PairViewSegment * Int] [UNI]
    RELATION srcOrTgt[PairViewSegment * SourceOrTarget] [UNI]
    RELATION text[PairViewSegment * String] [UNI]
ENDPATTERN

PATTERN StaticRelationsFromFormalAmpersand LABEL "Static relations from FormalAmpersand"
    REPRESENT ConceptDefinition TYPE BIGALPHANUMERIC

    RELATION acdcpt[ConceptDef*ConceptName] [UNI] -- ^ The name of the concept for which this is the definition. If there is no such concept, the conceptdefinition is ignored.
    RELATION acddef2[ConceptDef*Meaning] [UNI]    -- ^ The textual definition of this concept in the old syntax (e.g. CONCEPT foo "This is the definition").
    RELATION acdmean[ConceptDef*Meaning]          -- ^ User-specified meanings, possibly more than one, for multiple languages in the new syntax (e.g. CONCEPT foo MEANING {+This is the definition+}
    RELATION acdfrom[ConceptDef*Pattern] [UNI]    -- ^ The name of the pattern or context in which this concept definition was made
    RELATION allConjuncts[Context*Conjunct] -- The conjuncts contained by a context
    RELATION allRoles[Context*Role] -- The roles contained by a context
    RELATION allRules[Rule*Context] -- The rules contained by a context
    RELATION allRules[Pattern*Rule] -- The rules contained by a pattern
    RELATION arg[UnaryTerm*Term] [UNI] -- 
    RELATION asMarkdown[Markup*Text] [UNI] -- 
    RELATION bind[BindedRelation*Relation] [UNI] -- 
    RELATION concepts[Pattern*Concept] -- 
    RELATION ctxcds[ConceptDef*Context] [UNI] -- 
    RELATION ctxds[Relation*Context] [UNI] -- All relations defined in a context but outside a pattern
    RELATION ctxrs[Rule*Context] [UNI]-- All rules defined in a context but outside a pattern
    RELATION conjunct[Conjunct*Term] -- 
    RELATION context[Concept*Context] [UNI] -- The context that contains the definition of a concept
    RELATION context[Interface*Context] [UNI] -- The context that contains an Interface
    RULE IDENTInterface LABEL "IDENT Interface" : name;name~ /\ context[Interface*Context];context~ |- I
    VIOLATION (TXT "Interface ", SRC name, TXT " in context ", SRC context;name, TXT " is not unique.")
    RELATION context[Isa*Context] [UNI]
    RELATION context[IsE*Context] [UNI]
    RELATION context[Pattern*Context] [UNI] -- The context that contains a Pattern
    RELATION context[Population*Context] [UNI] -- The context that contains a Population
    RELATION context[Pattern*Context] [UNI] -- The context that contains a pattern
    RELATION context[Population*Context] [UNI] -- The context that contains a Population
    RELATION context[Rule*Context] [UNI] -- The context that contains a Rule
    RELATION declaredIn[Relation*Pattern] -- The Pattern in which a relation is declared
    RELATION declaredthrough[PropertyRule*Property] [TOT]
    MEANING "A property is defined as part of the declaration of relation."
    RELATION decMean[Relation * Meaning]
    MEANING "The meanings of a relation."
    RELATION decprL[Relation * String] [UNI]
    MEANING "The left part of a PRAGMA of a relation."
    RELATION decprM[Relation * String] [UNI]
    MEANING "The middle part of a PRAGMA of a relation."
    RELATION decprR[Relation * String] [UNI]
    MEANING "The right part of a PRAGMA of a relation."
    RELATION edit[FieldDef * Relation] -- to be filled automatically
    RELATION editFlp[FieldDef * Relation] -- to be filled automatically
    RELATION fieldIn[FieldDef * ObjectDef]
    RELATION first[BinaryTerm * Term] [UNI]
    RELATION formalTerm[Rule * Term] [UNI]
    MEANING "The term whose complement defines the violations of a rule."
    RELATION gengen[Isa*Concept] [UNI]
    MEANING "A generalization rule refers to one or more generic concepts."
    RELATION gengen[IsE * Concept] [TOT]
    RELATION genspc[Isa*Concept] [UNI]
    MEANING "A generalization rule refers to one or more specific concepts."
    RELATION genspc[IsE*Concept] [UNI]
    MEANING "A generalization rule refers to one or more specific concepts."
    RELATION identityRules[Rule*Context]
    RELATION identityRules[Rule*Pattern]
    RELATION ifcConjuncts[Interface*Conjunct]
    RELATION ifcObj[Interface*ObjectDef] [UNI]
    RELATION ifcPos[Interface*Origin] [UNI]
    RELATION ifcPurpose[Interface*Purpose]
    RELATION ifcQuads[Interface*Quad]
    RELATION ifcRoles[Interface*Role]
    RELATION pf_ifcRoles[PF_Interface*Role]
    RELATION isa[Concept*Concept]
    RELATION isAPI[PF_Interface*PF_Interface]
    RELATION isPublic[Interface*Interface]
    RELATION label[FieldDef*FieldName] [UNI]
    RELATION label[PF_Interface*PF_Label] [UNI]
    RELATION language[Context*Language] [UNI]
    RELATION language[Markup*Language] [UNI]
    RELATION maintains[Role*Rule]
    RELATION markup[Meaning*Markup] [UNI]
    RELATION markup[Purpose*Markup] [UNI]
    RELATION meaning[Rule*Meaning]
    MEANING "The meanings of a relation."
    RELATION message[Rule*Message] [INJ]
    RELATION proprules[PropertyRule*Context]
    RELATION proprules[PropertyRule*Pattern]
    RELATION name[Concept*ConceptName] [UNI]
    RELATION name[Context*ContextName] [UNI]
    RELATION name[Interface*InterfaceName] [UNI]
    RELATION name[ObjectDef*ObjectName] [UNI]
    RELATION name[Pattern*PatternName] [UNI]
    RELATION name[Relation*RelationName] [UNI]
    RELATION name[View*ViewDefName] [UNI,TOT]
    MEANING "Every relation has a name by which it can be referenced within its Context(s)."
    RELATION name[Role*RoleName] [UNI]
    RELATION name[Rule*RuleName] [UNI]
    RELATION objView[ObjectDef*View] [UNI]
    RELATION objpos[ObjectDef*Origin] [UNI]
    RELATION operator[BinaryTerm*Operator] [UNI]
    RELATION operator[UnaryTerm*Operator] [UNI]
    RELATION origin[Rule*Origin] [UNI]
    RELATION rc_orgRules[Conjunct*Rule]
    RELATION pairView[Rule*PairView]
    RELATION acdpos[ConceptDef*Origin] [UNI]      -- ^ The position of this definition in the text of the Ampersand source (filename, line number and column number).
    RELATION prop[Relation*Property]
    RELATION propertyRule[Relation*PropertyRule] [SUR]
    MEANING "The properties of a relation."
    RELATION purpose[Concept * Purpose]
    MEANING "The purposes of a concept."
    RELATION purpose[Context * Purpose] [{-This is currently *NOT* INJ, see issue https://github.com/AmpersandTarski/Ampersand/issues/46 -}]
    MEANING "The purposes of a context."
    RELATION purpose[Interface * Purpose]
    MEANING "The purposes of an interface."
    RELATION purpose[Pattern*Purpose]
    MEANING "The purposes of a pattern."
    RELATION purpose[Relation * Purpose]
    MEANING "The purposes of a relation."
    RELATION purpose[Rule * Purpose]
    MEANING "The purposes of a rule."
    RELATION purpose[View * Purpose]
    MEANING "The purposes of a view."
    RELATION qConjuncts[Quad*Conjunct]
    RELATION qDcl[Quad*Relation] [UNI]
    RELATION qRule[Quad*Rule] [UNI]
    RELATION relsDefdIn[Pattern*Relation]
    RELATION relsDefdIn[Relation*Context]
    RELATION second[BinaryTerm * Term] [UNI]
    RELATION showADL[Term * ShowADL] [UNI]
    RELATION sign[Term * Signature] [UNI]
    RELATION sign[Relation * Signature] [UNI]
    RELATION singleton[Singleton * Signature] [UNI]
    RELATION source[Relation*Concept] [UNI]
    RELATION src[Signature*Concept] [UNI]
    RELATION source[Relation*Concept] [UNI]
    RELATION target[Relation*Concept] [UNI]
    RELATION tgt[Signature*Concept] [UNI]
    RELATION ttype[Concept*TType] [UNI]
    RELATION udefrules[Rule*Context] [UNI]
    RELATION udefrules[Rule*Pattern] [UNI]
    REPRESENT EncodedName TYPE ALPHANUMERIC
    RELATION urlEncodedName[Concept*EncodedName] [UNI]
    RELATION urlEncodedName[Pattern*EncodedName] [UNI]
    RELATION urlEncodedName[Rule*EncodedName] [UNI]
    RELATION usedIn[Relation*Term]
    RELATION userSrc[Term*Concept] [UNI]
    RELATION userTgt[Term*Concept] [UNI]
    RELATION uses[Context*Pattern]
    RELATION versionInfo[Context*AmpersandVersion] [UNI]
    RELATION viewBy[Concept*Concept]
    RELATION vdats[View*ViewSegment]
    RELATION vdcpt[View*Concept]
    RELATION vdhtml[View*Concept]
    RELATION vdIsDefault[View*Concept]
    RELATION vdpos[View*Origin]
    RELATION violatable[Interface*Rule]
    -- EIGEN DINGEN
    RELATION concept[ConceptDef*Concept] [UNI,TOT]
    RELATION meaning[ConceptDef*Meaning] [UNI,TOT]
    RELATION showADL[PairView*ShowADL] [UNI]
ENDPATTERN

PATTERN RegistrationOfConceptualDiagrams LABEL "Registration of conceptual diagrams" --later to be moved to Scripts.adl (This is only here because of ease of development)

RELATION conceptualDiagram[Concept*FileObject] [UNI]
RELATION conceptualDiagram[Rule*FileObject] [UNI]
RELATION conceptualDiagram[Pattern*FileObject] [UNI]
RELATION conceptualDiagram[Context*FileObject] [UNI]

RELATION basePath[ScriptVersion*FilePath] [UNI] -- The path on the filesystem relative from RAP4 to the context stuff of this scriptversion ( i.e.:  scripts\123456\Script_1475844318716509\Version2016-10-07T144605\)

ROLE ExecEngine MAINTAINS linkcdofcontext
RULE linkcdofcontext LABEL "link cd of context" : I[Context] /\ context[ScriptVersion*Context]~;context[ScriptVersion*Context]
                               |- conceptualDiagram[Context*FileObject];conceptualDiagram[Context*FileObject]~
MEANING "Elke context in een ampersand-script wordt gevisualiseerd door middel van een conceptueel diagram"
VIOLATION ( TXT "{EX} InsAtom;FileObject"
          , TXT "{EX} InsPair;conceptualDiagram;Context;",SRC I[Context],TXT ";FileObject;_NEW"
          , TXT "{EX} InsPair;originalFileName;FileObject;_NEW;FileName;LogicalDataModel"
          , TXT "{EX} InsPair;filePath;FileObject;_NEW;FilePath;"
                                 , SRC context[ScriptVersion*Context]~;
                                       basePath[ScriptVersion*FilePath]
                                 , TXT "/images/LogicalDataModel.png"
          )

ROLE ExecEngine MAINTAINS linkcdofpattern
RULE linkcdofpattern LABEL "link cd of pattern" : I[Pattern] /\ context[Pattern*Context];context[ScriptVersion*Context]~;context[ScriptVersion*Context];context[Pattern*Context]~
                               |- conceptualDiagram[Pattern*FileObject];conceptualDiagram[Pattern*FileObject]~
MEANING "Elk pattern in een ampersand-script wordt gevisualiseerd door middel van een conceptueel diagram"
VIOLATION ( TXT "{EX} InsAtom;FileObject"
          , TXT "{EX} InsPair;conceptualDiagram;Pattern;",SRC I[Pattern],TXT ";FileObject;_NEW"
          , TXT "{EX} InsPair;originalFileName;FileObject;_NEW;FileName;ConceptueelDiagram"
          , TXT "{EX} InsPair;filePath;FileObject;_NEW;FilePath;"
                                 , SRC context[Pattern*Context];
                                       context[ScriptVersion*Context]~;
                                       basePath[ScriptVersion*FilePath]
                                 , TXT "/images/CDPattern"
                                 , SRC urlEncodedName
                                 , TXT ".png"
          )

ROLE ExecEngine MAINTAINS linkcdofrule
RULE linkcdofrule LABEL "link cd of rule" : I[Rule] /\ allRules[Rule*Context];context[ScriptVersion*Context]~;context[ScriptVersion*Context];allRules[Rule*Context]~
                               |- conceptualDiagram[Rule*FileObject];conceptualDiagram[Rule*FileObject]~
MEANING "Elke regel in een ampersand-script wordt gevisualiseerd door middel van een conceptueel diagram"
VIOLATION ( TXT "{EX} InsAtom;FileObject"
          , TXT "{EX} InsPair;conceptualDiagram;Rule;",SRC I[Rule],TXT ";FileObject;_NEW"
          , TXT "{EX} InsPair;originalFileName;FileObject;_NEW;FileName;ConceptueelDiagram"
          , TXT "{EX} InsPair;filePath;FileObject;_NEW;FilePath;"
                                 , SRC allRules[Rule*Context];
                                       context[ScriptVersion*Context]~;
                                       basePath[ScriptVersion*FilePath]
                                 , TXT "/images/CDRule"
                                 , SRC urlEncodedName
                                 , TXT ".png"
          )

ROLE ExecEngine MAINTAINS linkcdofconcept
RULE linkcdofconcept LABEL "link cd of concept" : I[Concept] /\(context[Concept*Context];context[ScriptVersion*Context]~;context[ScriptVersion*Context];context[Concept*Context]~)
                               |- conceptualDiagram[Concept*FileObject];conceptualDiagram[Concept*FileObject]~
MEANING "Elk concept in een ampersand-script wordt gevisualiseerd door middel van een conceptueel diagram"
VIOLATION ( TXT "{EX} InsAtom;FileObject"
          , TXT "{EX} InsPair;conceptualDiagram;Concept;",SRC I[Concept],TXT ";FileObject;_NEW"
          , TXT "{EX} InsPair;originalFileName;FileObject;_NEW;FileName;ConceptueelDiagram"
          , TXT "{EX} InsPair;filePath;FileObject;_NEW;FilePath;"
                                 , SRC context[Concept*Context];
                                       context[ScriptVersion*Context]~;
                                       basePath[ScriptVersion*FilePath]
                                 , TXT "/images/CDConcept"
                                 , SRC urlEncodedName
                                 , TXT ".png"
          )

ENDPATTERN

ENDCONTEXT