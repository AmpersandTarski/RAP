CONTEXT RAP IN ENGLISH


    INTERFACE "Atlas population": I[ScriptVersion];context[ScriptVersion*Context] BOX
        [ 
              name                  : I[Context];name                                           cRud
            , patterns              : context[Pattern*Context]~                     INTERFACE "JsonPattern"
            , interfaces            : context[Interface*Context]~                   INTERFACE "JsonInterface"
            , objects               : context[Interface*Context]~                   INTERFACE "JsonObjectDef"
            , conceptsCtx           : context[Concept*Context]~     -(context~;concepts)    INTERFACE "JsonConcept"    -- all concepts within the Context - those in Patterns
            , representationsCtx    : context[Concept*Context]~     -(context~;concepts)    INTERFACE "JsonConcept"    -- all REPRESENTATIONS within the Context - those in Patterns      
            , rulesCtx              : (allRules~ -proprules~)        -(context~;allRules)    INTERFACE "JsonRule"       -- all rules within the Context - those in Patterns 
            , relationsCtx          : relsDefdIn[Relation*Context]~ -(context~;declaredIn~) INTERFACE "JsonRelation"   -- all relations within the Context - those in Pattern
            , purposes              : purpose[Context*Purpose] \/                 -- purposes of the context
                                        context[Interface*Context]~;purpose[Interface*Purpose]  INTERFACE "JsonPurpose"
                                        --todo: add view
                                        --todo: add ident
            , "versionInfo" : versionInfo                                   cRud
            , scripts       : context[ScriptVersion*Context]~               BOX <PROPBUTTON>       
            [ property  : adlRequest                                        cRUd
            -- , disabled : I /\ funcSpec;funcSpec~  cRud
            ]
        ]

    INTERFACE "JsonPattern" : I[Pattern]      BOX
    [
            name                : name                                          cRud
            , concepts          : concepts[Pattern*Concept]                     INTERFACE "JsonConcept"
            , representations   : concepts[Pattern*Concept]                     INTERFACE "JsonConcept"            
            , rules             : allRules[Pattern*Rule] - proprules~           INTERFACE "JsonRule"
            , relations         : declaredIn[Relation*Pattern]~                 INTERFACE "JsonRelation"
            , properties        : proprules[PropertyRule*Pattern]~;declaredthrough[PropertyRule*Property]       cRud
            , purposes          : purpose[Pattern*Purpose] \/                                     -- purpose of the pattern
                                    concepts[Pattern*Concept];purpose[Concept*Purpose] \/           -- purpose of the concepts
                                    declaredIn[Relation*Pattern]~;purpose[Relation*Purpose] \/      -- purpose of the relations
                                    (allRules[Pattern*Rule]-proprules~);purpose[Rule*Purpose] INTERFACE "JsonPurpose"      -- purpose of the rules
            -- , views         :   views[Pattern*Views]    cRud
    ]


    INTERFACE "JsonConcept" : I[Concept] BOX
        [
            name            : name[Concept*ConceptName]                 cRud
            ,definition     : name;acdcpt~                              cRud
            , type          : ttype[Concept*TType]                      cRud
        -- , definitionmean : name;acdcpt~;acdmean                      cRud
        -- , definitionold : name;acdcpt~;acddef2                       cRud
        ]

    INTERFACE "JsonPurpose" : I [Purpose] BOX
    [         
            conceptPurp          :       purpose[Concept*Purpose]~;name                  cRud
            , relationPurp       :       purpose[Relation*Purpose]~                      BOX
                [
                    relation            : name[Relation*RelationName]                    cRud
                    , sign              : sign[Relation*Signature]                       INTERFACE "JsonSignature"
                ]
            , rulePurp           :       purpose[Rule*Purpose]~;name                     cRud
            --, identPurp          :       purpose[Ident*Purpose]~;name                  cRud -- the purpose for IDENT is not yet passed to ATLAS
            , viewPurp           :       purpose[View*Purpose]~;name                     cRud
            , patternPurp        :       purpose[Pattern*Purpose]~;name                  cRud
            , interfacePurp      :       purpose[Interface*Purpose]~;name                cRud
            , contextPurp        :       purpose[Context*Purpose]~;name                  cRud                           
            , meaning               :       markup;asMarkdown                               cRud
    ]

    INTERFACE "JsonRelation" : I[Relation] BOX
       [    
         name            : name[Relation*RelationName]                                      cRud    -- name of the relation
        , sign           : sign[Relation*Signature]                                         INTERFACE "JsonSignature"        -- signature of the relation (source and target)
        , properties     : prop[Relation*Property]                                          cRud    -- properties of the relation ([uni],[tot])
        , meaning        : decMean[Relation*Meaning];markup[Meaning*Markup];asMarkdown[Markup*Text] cRud  -- todo: make this the actual text of the meaning
       ] 
    
    INTERFACE "JsonSignature" : I[Signature] BOX                                    -- signature of the relation (source and target)
        [ 
         source         : src[Signature*Concept];name[Concept*ConceptName]  cRud     -- actual name of the source
        ,target         : tgt[Signature*Concept];name[Concept*ConceptName]  cRud     -- actual name of the target
        ]


INTERFACE "JsonRule" : I[Rule] BOX                      -- todo: hulp vragen bij formal expression!!!!!
       [    
         name            : name[Rule*RuleName]                              cRud    -- name of the rule
        , formexp       : formalTerm[Rule*Term];showADL[Term * ShowADL]     cRud    -- formal expression of the Rule
        -- , properties     : prop[Relation*Property]                       cRud    -- properties of the relation ([uni],[tot])
        -- , meaning        : meaning[Rule*Meaning];markup[Meaning*Markup];asMarkdown[Markup*Text]        cRud    -- Actual text of the meaning
        , meaning        : meaning[Rule*Meaning];markup;asMarkdown          cRud    -- Actual text of the meaning
        , message        : message[Rule*Message]                            cRud    -- Message of the rule
       ]

INTERFACE "JsonProperty" : I[PropertyRule] BOX   -- on hold (samen doen met termen)
        [
            property        : declaredthrough[PropertyRule*Property]                 cRud
        ]

INTERFACE "JsonInterface" : I[Interface] BOX
    [
        name            : name[Interface*InterfaceName]                      cRud
        , roles         : ifcRoles[Interface*Role]                           cRud
        , isAPI         : ifcRoles;pf_ifcRoles~;isAPI                        cRud
        , conjunct      : ifcConjuncts[Interface*Conjunct]                   cRud
        , objectDef     : ifcObj[Interface*ObjectDef]                        INTERFACE "JsonObjectDef"
        , quad          : ifcQuads[Interface*Quad]                           cRud
        
    ]
INTERFACE "JsonObjectDef" : I [ObjectDef] BOX  --dit is een box
    [
        name            : name[ObjectDef*ObjectName]                            cRud
        ,view            : objView[ObjectDef*View]                              INTERFACE "JsonView"
        ,fields         : fieldIn[FieldDef * ObjectDef]~                        INTERFACE "JsonFieldDef"
    ]


INTERFACE "JsonFieldDef"    : I[FieldDef] BOX
    [
        name            : label[FieldDef*FieldName]                             cRud
        , def           : def[Field * FieldDef]~	                            cRud
        , edit          : edit[FieldDef * Relation]                             cRud
        , editFlp       : editFlp[FieldDef * Relation]                           cRud
        , fieldIn       : fieldIn[FieldDef * ObjectDef] cRud
    ]


INTERFACE "JsonView"    : I [View] BOX
[
    name                : name[View*ViewDefName]                              cRud
    ,viewSegment        : vdats[View*ViewSegment]                             cRud
    ,concept            : vdcpt[View*Concept]                                       cRud 
    --, objectDef     : objView[ObjectDef*View]~                                  INTERFACE "JsonObjectDef"                
]


ENDCONTEXT
