CONTEXT RAP4 IN ENGLISH

VIEW "ParseAsHtml" : CompileResponse HTML TEMPLATE "View-ParseAsHtml.html" ENDVIEW

PURPOSE INTERFACE MyScripts
{+ Ik wil als student een overzicht krijgen van mijn scripts,
waarin ik scripts kan maken, verwijderen en naar het script kan navigeren om erin te wijzigen,
om zodoende volwaardig beheer over mijn eigen scripts te kunnen doen.
+}
INTERFACE MyScripts FOR User: "_SESSION";sessionAccount cRud BOX <TABLE hideOnNoRecords>
   [ "" : submittor~ CRuD BOX <TABLE sortable hideOnNoRecords>
      [ content  : I[Script]  cRud
      , opdracht : assignment CRUd
      , tijdstip : submitted  cRud
      ]
   , " " : I-submittor~;submittor cRud BOX <TABLE hideOnNoRecords> [ "Klik op + (rechts boven in de menubalk) om een nieuw script te maken" : V[Account*A];" "[A] cRud ]
   ]

INTERFACE StudentScripts FOR Tutor : "_SESSION"[SESSION]#(I[Account]/\submittor~;submittor) cRud BOX <TABLE sortable>
    [ indiener : I cRud
    , scripts : submittor~ cRud BOX<TABLE sortable>  -- never empty, because of precondition I[Account]/\submittor~;submittor
       [ opdracht  : assignment cRud
       , content   : I cRud
       , ingediend : submitted cRud
       ]
    ]

INTERFACE "Nieuw script" FOR User : I[Script] CRuD BOX <FORM hideLabels>
   [ "Meta-info": I cRud BOX <TABLE sortable>
      [ indiener  : submittor cRud
      , opdracht  : assignment CRUd
      , ingediend : submitted cRud
      ]
   , "Content":  content cRUd
   , "Actual info": I cRud BOX
      [ compile : I BOX <PROPBUTTON>
                  [ -- the field 'disabled'  stays empty (e.g. I-I), so it is never disabled.
                    property : compileRequest
                  ]
      , "Compiler message"  : compileresponse cRud
      , controls: seqFirstItem ; I[ScriptVersion] cRud BOX <TABLE>
         [ "Functional spec. document" : I BOX <PROPBUTTON>
                           [ property : funcSpecRequest          cRUd
                           , disabled : I /\ funcSpec;funcSpec~  cRud  -- disabled if a functional spec. exists. If it is already available, there's no need to request it again.
--                         , hide: I /\ funcSpec;funcSpec~       cRud  -- hidden if a functional spec. exists. If it is already available, there's no need to request it again.
                           , "popovertext": V;"Click here to generate a functional specification in Word format"[PopoverText] cRud -- Don't forget to populate PopoverText with the string you specify here!
                           ]
         , "Atlas" :      I BOX <PROPBUTTON>
                           [ property : loadRequest              cRUd
                           , disabled : loadedInRAP4Ok           cRud  -- disabled if the context is already loaded in RAP4.
--                         , "hide": loadedInRAP4Ok              cRud  -- hidden if the context is already loaded in RAP4.
                           , "popovertext": V;"Click here to generate an atlas of your script."[PopoverText] cRud -- Don't forget to populate PopoverText with the string you specify here!
                           ]
         , "Prototype" :  I BOX <PROPBUTTON>
                           [ property : protoRequest             cRUd
                           , disabled : I /\ protoOk             cRud  -- disabled if the prototype already exists. If it is already available, there's no need to request it again.
--                         , "hide": I /\ protoOk                cRud  -- hidden if the prototype already exists. If it is already available, there's no need to request it again.
                           , "popovertext": V;"Click here to deploy your prototype."[PopoverText] cRud -- Don't forget to populate PopoverText with the string you specify here!
                           ]
         , "Message" : compileresponse[ScriptVersion*CompileResponse] cRud <ParseAsHtml>
         ]
      , "links to your results": seqFirstItem ; I[ScriptVersion] cRud BOX <TABLE>
         [ "Download document" : funcSpec                        cRud
         , "Atlas" :             context                         cRud LINKTO INTERFACE "Context"
         , "Prototype" :         protoOk;version~;proto          cRud
         ]
      ]
   ]

POPULATION PopoverText  CONTAINS
   [ "Click here to generate a functional specification in Word format"
   , "Click here to generate an atlas of your script."
   , "Click here to deploy your prototype."
   ]

ENDCONTEXT