CONTEXT RAP IN DUTCH
RELATION markup[Purpose*Markup][UNI,TOT]
RELATION asMarkdown[Markup*Text][UNI,TOT]

-- VIEW Context : Context(TXT "Context")

VIEW Contextname: Context(name)
VIEW Script: Script(TXT "Terug naar script")
VIEW MeaningContent: Meaning(markup;asMarkdown)
VIEW PurposeContent: Purpose(markup;asMarkdown)

INTERFACE "Context" FOR User, Tutor : I[Context] cRud BOX<FORM hideLabels>
      [ "Terug naar script" : context[ScriptVersion*Context]~;version~ cRud
      , "Gegevensmodel" : conceptualDiagram cRud <ShowConceptualDiagram>
      , ""     : I[Context] TABS
         [ "Concepten" : context[Pattern*Context]~;concepts[Pattern*Concept] \/ context[Concept*Context]~ INTERFACE "Concepts"
         , "Relaties"  : declaredIn[Relation*Context]~  INTERFACE "Relations"
         , "Regels"    : I
            TABS [ "gewone regels"          : allRules[Context*Rule] ;(I[Rule]-I[PropertyRule]) INTERFACE "Rules"
                 , "multipliciteits regels" : allRules[Context*Rule] ;         I[PropertyRule];I[Rule] INTERFACE "Rules"
                 ]
         , "Patterns"   : context [Pattern * Context]~ INTERFACE "Patterns"
         ]
  ]


INTERFACE "Concepts" FOR User, Tutor : I[Concept] cRud BOX <TABLE sortable hideOnNoRecords>
  [ "Concepts:" : I cRud LINKTO INTERFACE "Concept"
  , "Purpose:" : purpose[Concept*Purpose] cRud 
  ]

INTERFACE "Concept" FOR User, Tutor : I[Concept] cRud BOX
  [ "Concept:" : I cRud
  , "Purpose:" : purpose[Concept*Purpose] cRud 
  , "used in (relations):" : source~ \/ target~ cRud LINKTO INTERFACE "Relation"
  , "More generic concepts:" : genspc[Isa*Concept]~;gengen[Isa*Concept] \/ genspc[IsE*Concept]~;gengen[IsE*Concept] cRud LINKTO INTERFACE "Concept"
  , "More specific concepts:" :gengen[Isa*Concept]~;genspc[Isa*Concept] \/ gengen[IsE*Concept]~;genspc[IsE*Concept] cRud LINKTO INTERFACE "Concept"
  , "Conceptual diagram": conceptualDiagram cRud <ShowConceptualDiagram>
  , "Context" : context cRud
  ]

INTERFACE "Relations" FOR User, Tutor : I[Relation] cRud BOX <TABLE sortable hideOnNoRecords>
  [ "Relation:" : I cRud -- name[Relation*RelationName] cRud en daar moet dan nog een view bij.
  , "MEANING:" : decMean[Relation*Meaning] cRud 
  , "PURPOSE:" : purpose[Relation*Purpose] cRud 
  ]
INTERFACE "Relation" FOR User, Tutor : I[Relation] cRud BOX
  [ "Relation:" : I cRud
  , "Source concept:" : source cRud LINKTO INTERFACE "Concept"
  , "Target concept:" : target cRud LINKTO INTERFACE "Concept"
  , "Eigenschappen:" : propertyRule;declaredthrough cRud
  , "Properties:" : prop [Relation * Property] cRud
  , "MEANING:" : decMean[Relation*Meaning] cRud 
  , "PURPOSE:" : purpose[Relation*Purpose] cRud 
  , "Context" : context cRud
  ]

INTERFACE "Rules" FOR User, Tutor : I[Rule] cRud BOX <TABLE sortable hideOnNoRecords>
  [ "Regel:" : I[Rule] cRud LINKTO INTERFACE "Rule"
  , "Formele expressie:" : formalExpression cRud
  ]

INTERFACE "Rule" FOR User, Tutor : I[Rule] cRud BOX
  [ "Regel:" : I[Rule] cRud
  , "origin" : origin cRud  
  , "Formele expressie:" : formalExpression cRud
  , "MEANING:" : meaning[Rule*Meaning] cRud 
  , "PURPOSE:" : purpose[Rule*Purpose] cRud 
  , "Conceptual diagram": (I[Rule]-I[PropertyRule]);conceptualDiagram cRud <ShowConceptualDiagram>
  , "Context" : allRules[Context*Rule]~ cRud
  ]

CLASSIFY PropertyRule ISA Rule

INTERFACE "Patterns" FOR User, Tutor : I[Pattern] cRud BOX <TABLE sortable hideOnNoRecords>
  [ "Thema's:" : I cRud
  , "Purpose:" : purpose[Pattern*Purpose] cRud 
  ]

INTERFACE "Pattern" FOR User, Tutor : I[Pattern] cRud BOX
  [ "Themes:" : I cRud
  , "Conceptual diagram": conceptualDiagram cRud <ShowConceptualDiagram>
  , "Context" : context  cRud
  ]

VIEW ShowConceptualDiagram: FileObject { imgfile : filePath }
HTML TEMPLATE "ShowConceptualDiagram.html"
ENDVIEW

PURPOSE INTERFACE "Language"
{+This interface shows the business language from one PATTERN.
Its purpose is printed first to introduce the corresponding theme to the target audience.
It must correspond with chapter 2, the business language chapter, from the functional specifications as generated by the compiler.
+}
INTERFACE "Language" FOR User,  Tutor : I[Pattern] cRud BOX
  [ "Theme:" : I cRud
  , "Context" : context cRud
  , "Purpose:" : purpose[Pattern*Purpose] cRud
  , "Definitions:" : concepts[Pattern*Concept] cRud BOX
         [ "Concept" : name[Concept*ConceptName]
         , "Purpose" : purpose[Concept*Purpose] cRud
         ]
  ]

ENDCONTEXT




--  , "Gens" : context[Concept*Context]~ ;
--             (I[Concept]                                  /\
--                gengen[Gen*Concept]~;gengen[Gen*Concept]  /\
--              -(genspc[Gen*Concept]~;genspc[Gen*Concept])
--             )  
--  , "Roles" : allRoles[Context*Role]
--  , "allConjuncts" : allConjuncts[Context*Conjunct]
