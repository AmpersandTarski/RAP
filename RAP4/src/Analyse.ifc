CONTEXT RAP IN DUTCH
RELATION markup[Purpose*Markup][UNI]
RELATION markup[Meaning*Markup][UNI]
REPRESENT Text TYPE ALPHANUMERIC

RULE defaultMarkupPurpose : I[Purpose] |- markup;V

RELATION name[Context*ContextName] [UNI]
RELATION asMarkdown[Markup*Text][UNI,TOT]
RELATION origin[Rule*Origin]
RELATION relsDefdIn[Relation*Context]

VIEW Script: Script(TXT "Terug naar script")
VIEW MeaningContent: Meaning(markup;asMarkdown)
VIEW PurposeContent: Purpose(markup;asMarkdown)


INTERFACE Concepts FOR User, Tutor : I[Concept] cRud BOX <TABLE sortable hideOnNoRecords>
  [ "Concepts:" : I cRud LINKTO INTERFACE "Concept"
  , "Purpose:" : purpose[Concept*Purpose] cRud 
  ]

INTERFACE Concept FOR User, Tutor : I[Concept] cRud BOX
  [ "Concept:" : I cRud
  , "Purpose:" : purpose[Concept*Purpose] cRud 
  , "File Object for CD" : conceptualDiagram cRud
  , "File path for CD" : conceptualDiagram;filePath cRud
  , "Original file name for CD" : conceptualDiagram;originalFileName cRud
  , "Conceptual diagram": conceptualDiagram cRud <ShowConceptualDiagram>
  , "Context" : context cRud
  ]

INTERFACE Relations FOR User, Tutor : I[Relation] cRud BOX <TABLE sortable hideOnNoRecords>
  [ "Relation:" : I cRud -- name[Relation*RelationName] cRud en daar moet dan nog een view bij.
  , "MEANING:" : decMean[Relation*Meaning] cRud 
  , "PURPOSE:" : purpose[Relation*Purpose] cRud 
  ]
INTERFACE Relation FOR User, Tutor : I[Relation] cRud BOX
  [ "Relation:" : I cRud
  , "Source concept:" : source cRud LINKTO INTERFACE "Concept"
  , "Target concept:" : target cRud LINKTO INTERFACE "Concept"
  , "Eigenschappen:" : propertyRule;declaredthrough cRud
  , "Properties:" : prop [Relation * Property] cRud
  , "MEANING:" : decMean[Relation*Meaning] cRud 
  , "PURPOSE:" : purpose[Relation*Purpose] cRud 
  , "Context" : relsDefdIn cRud
  ]

INTERFACE Rules FOR User, Tutor : I[Rule] cRud BOX <TABLE sortable hideOnNoRecords>
  [ "Regel:" : I[Rule] cRud LINKTO INTERFACE "Rule"
  , "Formele expressie:" : formalTerm cRud
  ]

INTERFACE Rule FOR User, Tutor : I[Rule] cRud BOX
  [ "Regel:" : I[Rule] cRud
  , "origin" : origin cRud  
  , "Formele expressie:" : formalTerm cRud
  , "MEANING:" : meaning[Rule*Meaning] cRud 
  , "PURPOSE:" : purpose[Rule*Purpose] cRud 
  , "Conceptual diagram": (I[Rule]-I[PropertyRule]);conceptualDiagram cRud <ShowConceptualDiagram>
  , "Context" : allRules[Context*Rule]~ cRud
  ]

CLASSIFY PropertyRule, IdentityRule ISA Rule

INTERFACE Patterns FOR User, Tutor : I[Pattern] cRud BOX <TABLE sortable hideOnNoRecords>
  [ "Thema's:" : I cRud
  , "Purpose:" : purpose[Pattern*Purpose] cRud 
  ]

INTERFACE Pattern FOR User, Tutor : I[Pattern] cRud BOX
  [ "Themes:" : I                           cRud
  , "Conceptual diagram": conceptualDiagram cRud <ShowConceptualDiagram>
  , "Context" : context                     cRud
  ]

VIEW ShowConceptualDiagram: FileObject { imgfile : filePath }
HTML TEMPLATE "ShowConceptualDiagram.html"
ENDVIEW

PURPOSE INTERFACE "Language"
{+This interface shows the business language from one PATTERN.
Its purpose is printed first to introduce the corresponding theme to the target audience.
It must correspond with chapter 2, the business language chapter, from the functional specifications as generated by the compiler.
+}
INTERFACE "Language" FOR User,  Tutor : I[Pattern] cRud BOX
  [ "Theme:" : I cRud
  , "Context" : context cRud
  , "Purpose:" : purpose[Pattern*Purpose] cRud
  , "Definitions:" : concepts[Pattern*Concept] cRud BOX
         [ "Concept" : name[Concept*ConceptName]
         , "Purpose" : purpose[Concept*Purpose] cRud
         ]
  ]

ENDCONTEXT




--  , "Gens" : context[Concept*Context]~ ;
--             (I[Concept]                                  /\
--                gengen[Gen*Concept]~;gengen[Gen*Concept]  /\
--              -(genspc[Gen*Concept]~;genspc[Gen*Concept])
--             )  
--  , "Roles" : allRoles[Context*Role]
--  , "allConjuncts" : allConjuncts[Context*Conjunct]
