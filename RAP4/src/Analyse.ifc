CONTEXT RAP IN DUTCH
RELATION markup[Purpose*Markup][UNI]
RELATION markup[Meaning*Markup][UNI]
REPRESENT Text TYPE BIGALPHANUMERIC   -- Fix for AmpersandTarski/Ampersand issue #1199

RULE defaultMarkupPurpose : I[Purpose] |- markup;V
VIOLATION ( TXT "The markup of the purpose of Concept ",   SRC purpose[Concept*Purpose]~,   TXT "is not defined\n"
          , TXT "The markup of the purpose of Rule ",      SRC purpose[Rule*Purpose]~,      TXT "is not defined\n"
          , TXT "The markup of the purpose of Relation ",  SRC purpose[Relation*Purpose]~,  TXT "is not defined\n"
          , TXT "The markup of the purpose of Interface ", SRC purpose[Interface*Purpose]~, TXT "is not defined\n"
          , TXT "The markup of the purpose of Pattern ",   SRC purpose[Pattern*Purpose]~,   TXT "is not defined\n"
          , TXT "The markup of the purpose of Context ",   SRC purpose[Context*Purpose]~,   TXT "is not defined\n"
          , TXT "The markup of the purpose of View ",      SRC purpose[View*Purpose]~,      TXT "is not defined\n"
          )

RELATION name[Context*ContextName] [UNI]
RELATION asMarkdown[Markup*Text][UNI,TOT]
RELATION origin[Rule*Origin]
RELATION relsDefdIn[Relation*Context]

VIEW MeaningContent: Meaning(markup;asMarkdown)
VIEW PurposeContent: Purpose(markup;asMarkdown)


INTERFACE Concept FOR User, Tutor : I[Concept] cRud BOX
  [ "Concept:" : I cRud
  , "Definition" : name;acdcpt~;(acddef2\/acdmean) cRud -- acddef2 is old syntax, acdmean is new syntax.
  , "Purpose:" : purpose[Concept*Purpose]          cRud 
  , "Used in rules": conceptualDiagram             cRud <ShowConceptualDiagram>
  , "Context" : context                            cRud
  ]

INTERFACE Relation FOR User, Tutor : I[Relation] cRud BOX
  [ "Relation:" : I cRud
  , "Source concept:" : source cRud LINKTO INTERFACE Concept
  , "Target concept:" : target cRud LINKTO INTERFACE Concept
  , "Eigenschappen:" : propertyRule;declaredthrough cRud
  , "Properties:" : prop [Relation * Property] cRud
  , "MEANING:" : decMean[Relation*Meaning] cRud 
  , "PURPOSE:" : purpose[Relation*Purpose] cRud 
  , "Context" : relsDefdIn cRud
  ]

VIEW Rule : Rule(name)

INTERFACE Rule FOR User, Tutor : I[Rule] cRud BOX
  [ "Regel:" : I[Rule] cRud
  , "origin" : origin cRud  
  , "Formele expressie:" : formalTerm cRud
  , "MEANING:" : meaning[Rule*Meaning] cRud 
  , "PURPOSE:" : purpose[Rule*Purpose] cRud 
  , "Conceptual diagram": conceptualDiagram cRud <ShowConceptualDiagram>
  , "Context" : allRules[Rule*Context] cRud
  ]

INTERFACE PropRule FOR User, Tutor : I[PropertyRule] cRud BOX
  [ "Regel:" : I[Rule] cRud
  , "origin" : origin cRud  
  , "Formele expressie:" : formalTerm cRud
  , "MEANING:" : meaning[Rule*Meaning] cRud 
  , "Context" : allRules[Rule*Context] cRud
  ]

CLASSIFY PropertyRule, IdentityRule ISA Rule

INTERFACE Patterns FOR User, Tutor : I[Pattern] cRud BOX <TABLE sortable hideOnNoRecords>
  [ "Thema:" : I cRud
  , "Purpose:" : purpose[Pattern*Purpose] cRud 
  ]

INTERFACE Pattern FOR User, Tutor : I[Pattern] cRud BOX
  [ "Thema:" : I                            cRud
  , "Conceptual diagram": conceptualDiagram cRud <ShowConceptualDiagram>
  , "Context" : context                     cRud
  ]

VIEW ShowConceptualDiagram: FileObject { imgfile : filePath }
HTML TEMPLATE "ShowConceptualDiagram.html"
ENDVIEW

PURPOSE INTERFACE Language
{+This interface shows the business language from one PATTERN.
Its purpose is printed first to introduce the corresponding theme to the target audience.
It must correspond with chapter 2, the business language chapter, from the functional specifications as generated by the compiler.
+}
INTERFACE Language FOR User,  Tutor : I[Pattern] cRud BOX
  [ "Theme:" : I cRud
  , "Context" : context cRud
  , "Purpose:" : purpose[Pattern*Purpose] cRud
  , "Definitions:" : concepts[Pattern*Concept] cRud BOX
         [ "Concept" : name[Concept*ConceptName]
         , "Purpose" : purpose[Concept*Purpose] cRud
         ]
  ]

ENDCONTEXT




--  , "Gens" : context[Concept*Context]~ ;
--             (I[Concept]                                  /\
--                gengen[Gen*Concept]~;gengen[Gen*Concept]  /\
--              -(genspc[Gen*Concept]~;genspc[Gen*Concept])
--             )  
--  , "Roles" : allRoles[Context*Role]
--  , "allConjuncts" : allConjuncts[Context*Conjunct]
