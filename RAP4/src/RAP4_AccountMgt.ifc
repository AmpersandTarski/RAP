CONTEXT "RAP4 AccountMgt" IN ENGLISH
-----------------------------------------------------------

VIEW Accounts: Account DEFAULT { "uid": accUserid } ENDVIEW

--[Account extensions]--

--CLASSIFY Studentnummer ISA Userid   -- NOTE: Userid is of TYPE ALPHANUMERIC, which Studentnummer inherits
RELATION accUserid[Account * Userid] [INJ,UNI] -- At most one account for every Studentnummer and vice versa
RELATION accPerson[Account * Person] [UNI] -- At most one name for every Studentnummer and vice versa
RELATION accEmail[Account * Emailaddress] [UNI] REPRESENT Emailaddress TYPE ALPHANUMERIC -- dit kan later een CEP-adres worden

--[Password (re)setting]--

ROLE ExecEngine MAINTAINS "Het default wachtwoord voor accounts is 'welkom'"
RULE "Het default wachtwoord voor accounts is 'welkom'": I[Account] |- accPassword;accPassword~
VIOLATION (TXT "{EX}InsPair;accPassword;Account;", SRC I, TXT ";Password;welkom")

-- The following has been removed, because it is patronizing code.
--ROLE User MAINTAINS "Wilt u uw wachtwoord veranderen?"
--RULE "Wilt u uw wachtwoord veranderen?": "_SESSION";sessionAccount;accPassword;"welkom" |- -V
--MESSAGE "Het wachtwoord 'welkom' dient te worden aangepast."
--VIOLATION (TXT "U dient uw wachtwoord te wijzigen in de interface 'MyAccount' in de menubalk.")

accNewPassword :: Account * Password [UNI]
ROLE ExecEngine MAINTAINS "(Re)set the password of an account"
RULE "(Re)set the password of an account": 
   accNewPassword /\ (sessionAccount \/ sessionActiveRoles;"AccountManager";V)~;"_SESSION";V |- -V
MEANING "(Re)setting the password for an account can only be done by an AccountManager or the account user."
VIOLATION ( TXT "{EX} InsPair;accPassword;Account;", SRC I, TXT ";Password;", TGT I
          , TXT "{EX} DelPair;accNewPassword;Account;", SRC I, TXT ";Password;", TGT I
          )
RULE "(Re)setting the password for an account can only be done by an AccountManager or the account user":
   accNewPassword |- (sessionAccount \/ sessionActiveRoles;"AccountManager";V)~;"_SESSION";V

--[AccountManager Interfaces]--

POPULATION Role CONTAINS [ "AccountManager" ]

INTERFACE "Accounts" FOR AccountManager: "_SESSION";V[SESSION*Account] CRuD BOX <TABLE sortable>
   [ "Userid": I cRud LINKTO INTERFACE "Account"
   , "Persoon"           : accPerson CRUD
   , "Toegestane rollen" : accAllowedRoles cRud
   , "Default rollen"    : accDefaultRoles cRud
   , "Persoonlijk ID"    : accUserid CRUD
   ]
           
INTERFACE "Account" FOR AccountManager: I[Account] cRud BOX
   [ "Userid": accUserid cRUd
   , "(Re)set password": accNewPassword cRUd -- crUd is needed for Passwords
-- , "Organization": accOrg cRUd
   , "Person": accPerson cRUd
   , "Allowed Roles": accAllowedRoles cRUd
   , "Default rollen": accDefaultRoles cRUd
   ]

PURPOSE INTERFACE "Roles"
{+This interface allows account manager to change assigned roles to accounts.
+}
INTERFACE "Roles" FOR AccountManager: "_SESSION";V[SESSION*Role] cRud BOX <TABLE sortable>
   [ "Role": I cRud
   , "Assigned to": accAllowedRoles~ cRUd
   , "Default for": accDefaultRoles~ cRud
   ]


--[User account management]--

PURPOSE INTERFACE "MyAccount"
{+This interface provides basic account information and allows a user to change his or her password.
+}
INTERFACE "MyAccount" FOR "User": "_SESSION" cRud BOX <FORM hideLabels>
   [ "no session account": (I-sessionAccount;sessionAccount~);V;"Editing your account requires that you are logged in"[LoginMessage] cRud
   , "session account": sessionAccount cRud BOX
      [ "Userid": accUserid cRud
      , "Nieuw wachtwoord": sessionAccount~ cRud BOX <TABLE> -- crUd is required for Passwords
         [ "Nieuw": regPassword1 cRUd -- crUd is required for Passwords
         , "(herhaal)": regPassword2 cRUd -- crUd is required for Passwords
         ]
      , "Persoon": accPersonRef cRud
      , "Toegestane rollen": accAllowedRoles cRud
      ]
   ]
POPULATION LoginMessage CONTAINS [ "Editing your account requires that you are logged in" ]

PURPOSE RULE "Remove expired sessions" MARKDOWN
{+When a browser-session ceases to exist, RAP4 still has a link between session and account.
The next time a session is linked to this account, the previous sessions should be removed.
+}
ROLE ExecEngine MAINTAINS "Remove expired sessions"
RULE "Remove expired sessions":
   "_SESSION";sessionAccount;sessionAccount~ |- "_SESSION"
VIOLATION (TXT "{EX} DelAtom;SESSION;", TGT I)

--[Studenten mogen een eigen account aanmaken door dat aan te vinken bij de login]--
RELATION regUserID      [SESSION * Userid]       [UNI] -- Studentnummer waarmee de student zich wil registreren
RELATION regPassword1   [SESSION * Password]     [UNI]
RELATION regPassword2   [SESSION * Password]     [UNI]
RELATION regEmail       [SESSION * Emailaddress] [UNI] -- OU Emailaccount (kunnen we later met Messaging gebruiken)
RELATION regPerson      [SESSION * Person]       [UNI]
RELATION registerButton [SESSION * SESSION]      [PROP] -- knopje om expliciet te zeggen dat met deze gegevens geregistreerd moet gaan worden.
RELATION loginButton    [SESSION * SESSION]      [PROP]      -- knopje om expliciet te zeggen dat de gebruiker wil inloggen.

PURPOSE RULE "Login upon request"
{+This rule links the current session to one account,
being the account that corresponds to the `loginUserid` and `loginPassword` fields.
+}
ROLE ExecEngine MAINTAINS "Login upon request"
RULE "Login upon request": 
   ("_SESSION" - sessionAccount;sessionAccount~)          -- the current session is not logged in,
   /\ loginUserid;accUserid~;accPassword;loginPassword~   -- the password and userid match to the account, and
   /\ loginButton                                         -- the login button is pressed
|-     sessionAccount;sessionAccount~
VIOLATION ( TXT "{EX} InsPair;sessionAccount;SESSION;", SRC I, TXT ";Account;", SRC loginUserid;accUserid~ 
          , TXT "{EX} DelPair;loginUserid;SESSION;", SRC I, TXT ";Userid;", SRC loginUserid
          , TXT "{EX} DelPair;loginPassword;SESSION;", SRC I, TXT ";Password;", SRC loginPassword
          )

--[Studenten mogen een eigen account aanmaken middels een specifieke interface]--
ROLE ExecEngine MAINTAINS "Login on a new account when registering"
RULE "Login on a new account when registering": 
   (registerButton - sessionAccount;sessionAccount~)  -- button is pushed with no-one logged in
   /\ regUserID;-accUserid~;V[Account*SESSION]        -- the new userID does not exist
   /\ regPassword1;regPassword2~                      -- both passwords are equal
|-    sessionAccount;sessionAccount~
VIOLATION ( TXT "{EX} InsAtom;Account"
          , TXT "{EX} InsPair;accUserid;Account;_NEW;Userid;", TGT regUserID
          , TXT "{EX} InsPair;sessionAccount;SESSION;", SRC I, TXT ";Account;_NEW"
          , TXT "{EX} DelPair;regUserID;SESSION;", SRC I, TXT ";Userid;", TGT regUserID
          , TXT "{EX} DelPair;registerButton;SESSION;", SRC I, TXT ";SESSION;", TGT I
          )

--[Users may change their password when logged in]--
ROLE ExecEngine MAINTAINS "Change password"
RULE "Change password": 
   ("_SESSION" /\ sessionAccount;sessionAccount~)
   /\ regPassword1;regPassword2~
|-    sessionAccount;accPassword;regPassword2~
VIOLATION ( TXT "{EX} InsPair;accPassword;Account;", SRC sessionAccount, TXT ";Password;", TGT regPassword2
          , TXT "{EX} DelPair;regPassword1;SESSION;", SRC I, TXT ";Password;", TGT regPassword1
          , TXT "{EX} DelPair;regPassword2;SESSION;", SRC I, TXT ";Password;", TGT regPassword2
          )

RULE CopyEmail : sessionAccount~;registerButton;regEmail |- accEmail
VIOLATION ( TXT "{EX} InsPair;accEmail;Account;",  SRC I, TXT ";Emailaddress;", TGT I )
RULE EmptyRegEmail : sessionAccount;accEmail |- -regEmail
VIOLATION ( TXT "{EX} DelPair;regEmail;SESSION;",  SRC I, TXT ";Emailaddress;", TGT I )
RULE CopyPerson : sessionAccount~;registerButton;regPerson |- accPerson
VIOLATION ( TXT "{EX} InsPair;accPerson;Account;",  SRC I, TXT ";Person;", TGT I )
RULE EmptyRegPerson : sessionAccount;accPerson |- -regPerson
VIOLATION ( TXT "{EX} DelPair;regPerson;SESSION;",  SRC I, TXT ";Person;", TGT I )
RULE ResetCreateAccount : (registerButton /\ sessionAccount;sessionAccount~)-(regEmail;regEmail~\/regPerson;regPerson~) |- -registerButton
VIOLATION ( TXT "{EX} DelPair;registerButton;SESSION;", SRC I, TXT ";SESSION;", TGT I )

PURPOSE RULE RefuseExistingStudentnumber
{+Registering a student number that is already known yields an error message,
asking to register with a student number that is not yet registered as a user.
+}
RULE RefuseExistingStudentnumber : ("_SESSION"-sessionAccount;sessionAccount~);regUserID;(I/\accUserid~;accUserid) |- V[SESSION*Account];-accUserid
VIOLATION (TXT "You cannot register student number ", SRC regUserID, TXT ", because it is registered already.")
RULE EqualPasswords : regPassword1~;regPassword2 |- I
MESSAGE "Please type equal passwords (case sensitive)"
ROLE ExecEngine MAINTAINS CopyEmail, EmptyRegEmail, CopyPerson, EmptyRegPerson, ResetCreateAccount


{- Toestanden
1	"_SESSION";regUserID;-accUserid~;V	A new userid has been entered in this session's register menu. So, someone wants to register a new account.
2	"_SESSION";regUserID;accUserid~;V	An existing userid has been entered in this session's register menu. Apparently, someone wants to register a new account, but it exists already.
3	"_SESSION";(I/\regPassword1;regPassword2~);V	Two equal passwords have been entered in this session's register menu. So, this is the password for the new account
3	"_SESSION";(I/\regPassword1;-I;regPassword2~);V	Two unequal passwords have been entered in this session's register menu. Apparently, the user has mis-typed the password.
4	"_SESSION";regEmail;V	An e-mail address has been entered in this session's register menu. This email address will be used for the new account
5	"_SESSION";registerButton;V	The "Registreer"-button has been clicked on in this session's register menu. So, the user is done and wishes to make the new account
6	"_SESSION";(I/\sessionAccount;sessionAccount~);V	This session is logged in.
7	"_SESSION";(I-sessionAccount;sessionAccount~);V	This session is not logged in.
8	"_SESSION";(I/\loginUserid;accUserid~;accPassword;loginPassword~);V	The user has provided the username/password combination of an existing account. So, we can log this user into the system.

State transitions
Login	7, 8 --> 6
Register	1, 3, 4, 5 --> 6
-}

ENDCONTEXT