CONTEXT "RAP3 AccountMgt" IN ENGLISH
-----------------------------------------------------------

VIEW Accounts: Account DEFAULT { "uid": accUserid } ENDVIEW

--[Account extensions]--

--CLASSIFY Studentnummer ISA UserID   -- NOTE: UserID is of TYPE ALPHANUMERIC, which Studentnummer inherits
accUserid :: Account * UserID [INJ,UNI] -- At most one account for every Studentnummer and vice versa
accPerson :: Account * Person [UNI] -- At most one name for every Studentnummer and vice versa
accEmail :: Account * Emailaddress [UNI] REPRESENT Emailaddress TYPE ALPHANUMERIC -- dit kan later een CEP-adres worden

ROLE ExecEngine MAINTAINS "Accounts with a UserID are assigned the role of 'Student'"
RULE "Accounts with a UserID are assigned the role of 'Student'": accUserid # "Student"[Role] |- accAllowedRoles
VIOLATION (TXT "{EX}InsPair;accAllowedRoles;Account;", SRC I, TXT ";Role;", TGT I)

ROLE ExecEngine MAINTAINS "Accounts without a UserID may not be assigned the role of 'Student'"
RULE "Accounts without a UserID may not be assigned the role of 'Student'": accAllowedRoles;"Student" |- accUserid;V
VIOLATION (TXT "{EX}DelPair;accAllowedRoles;Account;", SRC I, TXT ";Role;", TGT I)

--ROLE ExecEngine MAINTAINS "'Student' role is always a default role"
--RULE "'Student' role is always a default role": accAllowedRoles # "Student"[Role] |- accDefaultRoles
--VIOLATION (TXT "{EX}InsPair;accDefaultRoles;Account;", SRC I, TXT ";Role;", TGT I)

--[Password (re)setting]--

ROLE ExecEngine MAINTAINS "Het default wachtwoord voor accounts is 'welkom'"
RULE "Het default wachtwoord voor accounts is 'welkom'": I[Account] |- accPassword;accPassword~
VIOLATION (TXT "{EX}InsPair;accPassword;Account;", SRC I, TXT ";Password;welkom")

-- The following has been removed, because it is patronizing code.
--ROLE User MAINTAINS "Wilt u uw wachtwoord veranderen?"
--RULE "Wilt u uw wachtwoord veranderen?": "_SESSION";sessionAccount;accPassword;"welkom" |- -V
--MESSAGE "Het wachtwoord 'welkom' dient te worden aangepast."
--VIOLATION (TXT "U dient uw wachtwoord te wijzigen in de interface 'My Account' in de menubalk.")

accNewPassword :: Account * Password [UNI]
ROLE ExecEngine MAINTAINS "(Re)set the password of an account"
RULE "(Re)set the password of an account": 
   accNewPassword /\ (sessionAccount \/ sessionActiveRoles;"AccountManager";V)~;"_SESSION";V |- -V
MEANING "(Re)setting the password for an account can only be done by an AccountManager or the account user."
VIOLATION (TXT "{EX} InsPair;accPassword;Account;", SRC I, TXT ";Password;", TGT I
          ,TXT "{EX} DelPair;accNewPassword;Account;", SRC I, TXT ";Password;", TGT I
          )
RULE "(Re)setting the password for an account can only be done by an AccountManager or the account user":
   accNewPassword |- (sessionAccount \/ sessionActiveRoles;"AccountManager";V)~;"_SESSION";V

--[AccountManager Interfaces]--

POPULATION Role CONTAINS [ "AccountManager" ]

INTERFACE "Accounts" FOR AccountManager: "_SESSION";V[SESSION*Account] CRuD BOX <SCOLS>
   [ "Userid": I cRud LINKTO INTERFACE "Account"
   , "Persoon"           : accPerson CRUD
   , "Toegestane rollen" : accAllowedRoles cRud
   , "Default rollen"    : accDefaultRoles cRud
   , "Persoonlijk ID"    : accUserid CRUD
   ]
           
INTERFACE "Account" FOR AccountManager: I[Account] cRud ROWS
   [ "Userid": accUserid cRUd
   , "(Re)set password": accNewPassword crUd -- crUd is needed for Passwords
-- , "Organization": accOrg cRUd
   , "Person": accPerson cRUd
   , "Allowed Roles": accAllowedRoles cRUd
   , "Default rollen": accDefaultRoles cRUd
   ]

PURPOSE INTERFACE "Roles"
{+This interface allows account manager to change assigned roles to accounts.
+}
INTERFACE "Roles" FOR AccountManager: "_SESSION";V[SESSION*Role] cRud BOX <SCOLS>
   [ "Role": I cRud
   , "Assigned to": accAllowedRoles~ cRUd
   , "Default for": accDefaultRoles~ cRud
   ]


--[User account management]--

INTERFACE "My Account" FOR "User": "_SESSION" cRud BOX <ROWSNL>
   [ "no session account": (I-sessionAccount;sessionAccount~);V;"Editing your account requires that you are logged in"[LoginMessage] cRud
   , "session account": sessionAccount cRUd ROWS
      [ "Userid": accUserid cRud
      , "Nieuw wachtwoord": sessionAccount~ cRud COLS -- crUd is required for Passwords
         [ "Nieuw": regPassword1 crUd -- crUd is required for Passwords
         , "(herhaal)": regPassword2 crUd -- crUd is required for Passwords
         ]
      , "Persoon": accPerson CRUD
      , "Toegestane rollen": accAllowedRoles cRud
      , "Default rollen": accDefaultRoles cRUd
      , "Persoonlijk ID": accUserid cRUd  -- This field is needed to allow a student to provide his or her own student number.
      ]
   ]
POPULATION LoginMessage CONTAINS [ "Editing your account requires that you are logged in" ]

--[Studenten mogen een eigen account aanmaken door dat aan te vinken bij de login]--
regUserID        :: SESSION * UserID [UNI] -- Studentnummer waarmee de student zich wil registreren
regPassword1     :: SESSION * Password [UNI]
regPassword2     :: SESSION * Password [UNI]
regEmail         :: SESSION * Emailaddress [UNI] -- OU Emailaccount (kunnen we later met Messaging gebruiken)
regPerson        :: SESSION * Person [UNI]
regCreateAccount :: SESSION * SESSION [PROP] -- knopje om expliciet te zeggen dat met deze gegevens geregistreerd moet gaan worden.

ROLE ExecEngine MAINTAINS "Login upon request"
RULE "Login upon request": 
   ("_SESSION" - sessionAccount;sessionAccount~)
   /\ loginUserid;accUserid~;accPassword;loginPassword~
|-     sessionAccount;sessionAccount~
VIOLATION (TXT "{EX} InsPair;sessionAccount;SESSION;", SRC I, TXT ";Account;", SRC loginUserid;accUserid~ 
          ,TXT "{EX} DelPair;loginUserid;SESSION;", SRC I, TXT ";UserID;", SRC loginUserid
          ,TXT "{EX} DelPair;loginPassword;SESSION;", SRC I, TXT ";Password;", SRC loginPassword
          )

--[Studenten mogen een eigen account aanmaken middels een specifieke interface]--
ROLE ExecEngine MAINTAINS "Login on a new account when registering"
RULE "Login on a new account when registering": 
   (regCreateAccount - sessionAccount;sessionAccount~)
   /\ regUserID;-accUserid~;V[Account*SESSION]
   /\ regPassword1;regPassword2~
|-    sessionAccount;sessionAccount~
VIOLATION (TXT "{EX} NewStruct;Account"
               ,TXT ";sessionAccount;SESSION;", SRC I, TXT ";Account;_NEW"
          )

--[Studenten mogen een eigen account aanmaken middels een specifieke interface]--
ROLE ExecEngine MAINTAINS "Register a new account"
RULE "Register a new account":
   sessionAccount~;regUserID |- accUserid
VIOLATION (TXT "{EX} InsPair;accUserid;Account;", SRC I, TXT ";UserID;", TGT I
          ,TXT "{EX} DelPair;regUserID;SESSION;", SRC sessionAccount~, TXT ";UserID;", TGT I
          )

--[Users may change their password when logged in]--
ROLE ExecEngine MAINTAINS "Change password"
RULE "Change password": 
   ("_SESSION" /\ sessionAccount;sessionAccount~)
   /\ regPassword1;regPassword2~
|-    sessionAccount;accPassword;regPassword2~
VIOLATION (TXT "{EX} InsPair;accPassword;Account;", SRC sessionAccount, TXT ";Password;", TGT regPassword2
          ,TXT "{EX} DelPair;regPassword1;SESSION;", SRC I, TXT ";Password;", TGT regPassword1
          ,TXT "{EX} DelPair;regPassword2;SESSION;", SRC I, TXT ";Password;", TGT regPassword2
          )

RULE CopyEmail : sessionAccount~;regCreateAccount;regEmail |- accEmail
VIOLATION ( TXT "{EX} InsPair;accEmail;Account;",  SRC I, TXT ";Emailaddress;", TGT I )
RULE EmptyRegEmail : sessionAccount;accEmail |- -regEmail
VIOLATION ( TXT "{EX} DelPair;regEmail;SESSION;",  SRC I, TXT ";Emailaddress;", TGT I )
RULE CopyPerson : sessionAccount~;regCreateAccount;regPerson |- accPerson
VIOLATION ( TXT "{EX} InsPair;accPerson;Account;",  SRC I, TXT ";Person;", TGT I )
RULE EmptyRegPerson : sessionAccount;accPerson |- -regPerson
VIOLATION ( TXT "{EX} DelPair;regPerson;SESSION;",  SRC I, TXT ";Person;", TGT I )
RULE ResetCreateAccount : (regCreateAccount /\ sessionAccount;sessionAccount~)-(regEmail;regEmail~\/regPerson;regPerson~) |- -regCreateAccount
VIOLATION ( TXT "{EX} DelPair;regCreateAccount;SESSION;", SRC I, TXT ";SESSION;", TGT I )

PURPOSE RULE RefuseExistingStudentnumber
{+Registering a student number that is already known yields an error message,
asking to register with a student number that is not yet registered as a user.
+}
RULE RefuseExistingStudentnumber : ("_SESSION"-sessionAccount;sessionAccount~);regUserID;(I/\accUserid~;accUserid) |- V[SESSION*Account];-accUserid
VIOLATION (TXT "You cannot register student number ", SRC regUserID, TXT ", because it is registered already.")
RULE EqualPasswords : regPassword1~;regPassword2 |- I
MESSAGE "Please type equal passwords (case sensitive)"
ROLE ExecEngine MAINTAINS CopyEmail, EmptyRegEmail, CopyPerson, EmptyRegPerson, ResetCreateAccount


{- Toestanden
1	"_SESSION";regUserID;-accUserid~;V	A new userid has been entered in this session's register menu. So, someone wants to register a new account.
2	"_SESSION";regUserID;accUserid~;V	An existing userid has been entered in this session's register menu. Apparently, someone wants to register a new account, but it exists already.
3	"_SESSION";(I/\regPassword1;regPassword2~);V	Two equal passwords have been entered in this session's register menu. So, this is the password for the new account
3	"_SESSION";(I/\regPassword1;-I;regPassword2~);V	Two unequal passwords have been entered in this session's register menu. Apparently, the user has mis-typed the password.
4	"_SESSION";regEmail;V	An e-mail address has been entered in this session's register menu. This email address will be used for the new account
5	"_SESSION";regCreateAccount;V	The "Registreer"-button has been clicked on in this session's register menu. So, the user is done and wishes to make the new account
6	"_SESSION";(I/\sessionAccount;sessionAccount~);V	This session is logged in.
7	"_SESSION";(I-sessionAccount;sessionAccount~);V	This session is not logged in.
8	"_SESSION";(I/\loginUserid;accUserid~;accPassword;loginPassword~);V	The user has provided the username/password combination of an existing account. So, we can log this user into the system.

State transitions
Login	7, 8 --> 6
Register	1, 3, 4, 5 --> 6
-}

ENDCONTEXT