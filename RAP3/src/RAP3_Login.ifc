CONTEXT "Login" IN ENGLISH
-----------------------------------------------------------

ROLE ExecEngine MAINTAINS "Add role Anonymous to sessions"
RULE "Add role Anonymous to sessions": I[SESSION]-(sessionAccount;sessionAccount~) |- sessionAllowedRoles;"Anonymous";sessionAllowedRoles~
VIOLATION (TXT "{EX}InsPair;sessionAllowedRoles;SESSION;", SRC I, TXT ";Role;Anonymous")
-- When a user logs in, this role is removed by rule 'Clear allowed session roles'

ROLE ExecEngine MAINTAINS "Activate Anonymous by default"
RULE "Activate Anonymous by default": sessionAllowedRoles;"Anonymous" |- sessionActiveRoles;"Anonymous"
VIOLATION (TXT "{EX}InsPair;sessionActiveRoles;SESSION;", SRC I, TXT ";Role;Anonymous")

INTERFACE "Login": "_SESSION"[SESSION] cRud BOX <FORM hideOnNoRecords>
   [ "Login met een bestaand account" : I-sessionAccount;sessionAccount~ cRud BOX <TABLE hideOnNoRecords>
      [ "Studentnummer": loginUserid cRUd
      , "Password": loginPassword crUd -- crUd is needed for Passwords
      , "Login" : I BOX <PROPBUTTON>
                          [ disabled : I-I   -- was:   I-(loginUserid;loginUserid~ /\ loginPassword;loginPassword~) cRud -- Only active if name and password are entered
                          , property : loginButton cRUd
                          ]
      ]
   , "Registreer een nieuw account": I-sessionAccount;sessionAccount~ cRud BOX <TABLE hideOnNoRecords>
      [ "Studentnummer" : regUserID cRUd
      , "Email" :      regEmail cRUd
      , "Naam" :       regPerson cRud
      , "Wachtwoord" : regPassword1 crUd -- crUd is needed for Passwords
      , "(herhaal)" :  regPassword2 crUd -- crUd is needed for Passwords
      , "Registreer" : I BOX <PROPBUTTON>
                          [ disabled : I-(regPassword1;regPassword2~ /\ regUserID;regUserID~) cRud -- Only active if there are two equal passwords.
                          , property : registerButton cRUd
                          ]
      ]
   , "Logout": I /\ sessionAccount;sessionAccount~ cRud BOX <FORM hideLabels>
      [ "Logout": I cRud BOX <TABLE hideOnNoRecords>
         [ "Logout?": I BOX <PROPBUTTON>
                               [ disabled : I-I
                               , property : logoutRequest cRUd
                               ]
         , "Userid": sessionUserid cRud
--       , "Organization": accOrg cRud
--       , "Person": accPerson CRUD
         , "Email": sessionAccount;accEmail cRud
--       , "Allowed roles": sessionAllowedRoles cRUd
--       , "Active roles": sessionActiveRoles cRUd
         ]
      ]
   ]

ENDCONTEXT