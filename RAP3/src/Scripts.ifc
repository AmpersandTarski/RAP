CONTEXT RAP3 IN ENGLISH

PURPOSE INTERFACE MyScripts
{+ Ik wil als student een overzicht krijgen van mijn scripts,
waarin ik scripts kan maken, verwijderen en naar het script kan navigeren om erin te wijzigen,
om zodoende volwaardig beheer over mijn eigen scripts te kunnen doen.
+}
INTERFACE MyScripts FOR Student: "_SESSION";sessionAccount;submittor~ cRud BOX <SHCOLS>
-- Notice that the cRuD on line 8 must not be CRuD, because the green '+' generates a Slim error at run time. See issue #704.
    [ content  : I
    , opdracht : assignment
    , tijdstip : submitted cRud
    ]

INTERFACE StudentScripts FOR Tutor : "_SESSION"[SESSION]#(I[Account]/\submittor~;submittor) cRud BOX <SCOLS>
    [ indiener : I cRud
    , scripts : submittor~ cRud BOX<SCOLS>  -- never empty, because of precondition I[Account]/\submittor~;submittor
       [ opdracht  : assignment cRud
       , content   : I cRud
       , ingediend : submitted cRud
       ]
    ]

INTERFACE "Nieuw script" FOR Student, User : I[Script] CRuD BOX <ROWSNL>
   [ "Meta-info": I cRud BOX <SCOLS>
      [ indiener  : submittor cRud
      , opdracht  : assignment CRUd
      , ingediend : submitted cRud
      ]
   , "Content":  content cRUd
   , "Actual info": I cRud ROWS
      [ compile : I BOX <PROPBUTTON>
                  [ disabled : I-I            cRud
                  , property : compileRequest cRUd
                  ]
      , "Compiler message"  : compileresponse cRud
      , controls: seqFirstItem ; I[ScriptVersion] cRud COLS
         [ "Diagnosis" :  I BOX <PROPBUTTON>
                           [ disabled : (I/\diag;diag~) \/ (I-scriptOk)  cRud
                           , property : diagRequest cRUd
                           ]
         , "Func. spec + pictures" : I BOX <PROPBUTTON>
                           [ disabled : (I/\funcSpec;funcSpec~) \/ (I-scriptOk)  cRud
                           , property : funcSpecRequest cRUd
                           ]
-- Atlas kan erg lang duren. Wellicht in combinatie met het niet goed werken van multiuser functionaliteit? Uitzoeken! HJO, 20170423
         , "Atlas" :      I BOX <PROPBUTTON>
                           [ disabled : (I/\loadedInRAP3Ok) \/ (I-scriptOk) cRud -- Only active if there population has been loaded.
                           , property : loadRequest cRUd
                           ]
         , "Prototype" :  I BOX <PROPBUTTON>
                           [ disabled : (I/\protoOk) \/ (I-scriptOk)        cRud
                           , property : protoRequest cRUd
                           ]
         , "Message" : compileresponse[ScriptVersion*CompileResponse] cRud
         ]
      , "links to your results": seqFirstItem ; I[ScriptVersion] cRud COLS
         [ "Diagnosis"  : diag cRud
         , "Func. spec" : funcSpec cRud
         , "Atlas" :      context cRud LINKTO INTERFACE "Context"
         , "Prototype" :  protoOk;version~;proto cRud
         ]
      ]
   ]

ENDCONTEXT