CONTEXT RAP IN DUTCH

-- VIEW Context : Context(TXT "Context")

INTERFACE "Context" FOR User, Student,Tutor : I[Context] cRud BOX<ROWSNL>
      [ "Terug naar script" : context[ScriptVersion*Context]~;version~ cRud
      , "Gegevensmodel" : conceptualDiagram cRud <ShowConceptualDiagram>
      , ""     : I[Context] TABS
         [ "Concepten" : (context[Pattern*Context]~;concepts[Pattern*Concept]) \/ context[Concept*Context]~ INTERFACE "Concepts"
         , "Relaties"  : declaredIn[Relation*Context]~  INTERFACE "Relations"
         , "Regels"    : I
            TABS [ "gewone regels"          : allRules[Context*Rule] ;(I[Rule]-I[PropertyRule]) INTERFACE "Rules"
                 , "multipliciteits regels" : allRules[Context*Rule] ;         I[PropertyRule];I[Rule] INTERFACE "Rules"
                 ]
         , "Patterns"   : context [Pattern * Context]~ INTERFACE "Patterns"
         ]
  ]


INTERFACE "Concepts" FOR User, Student,Tutor : I[Concept] cRud BOX <SHCOLS>
  [ "Concepts:" : I cRud LINKTO INTERFACE "Concept"
  , "Purpose:" : purpose[Concept*Purpose] cRud 
  ]

INTERFACE "Concept" FOR User, Student,Tutor : I[Concept] cRud BOX
  [ "Concept:" : I cRud
  , "Purpose:" : purpose[Concept*Purpose] cRud 
  , "used in (relations):" : source~ \/ target~ cRud LINKTO INTERFACE "Relation"
  , "More generic concepts:" : genspc[Isa*Concept]~;gengen[Isa*Concept] \/ genspc[IsE*Concept]~;gengen[IsE*Concept] cRud LINKTO INTERFACE "Concept"
  , "More specific concepts:" :gengen[Isa*Concept]~;genspc[Isa*Concept] \/ gengen[IsE*Concept]~;genspc[IsE*Concept] cRud LINKTO INTERFACE "Concept"
  , "Conceptual diagram": conceptualDiagram cRud <ShowConceptualDiagram>
  ]

INTERFACE "Relations" FOR User, Student,Tutor : I[Relation] cRud BOX <SHCOLS>
  [ "Relation:" : I cRud -- name[Relation*RelationName] cRud en daar moet dan nog een view bij.
  , "Source concept:" : source cRud LINKTO INTERFACE "Concept"
  , "Target concept:" : target cRud LINKTO INTERFACE "Concept"
  , "Meaning:" : decMean[Relation*Meaning] cRud 
  ]
INTERFACE "Relation" FOR User, Student,Tutor : I[Relation] cRud BOX
  [ "Relation:" : I cRud
  , "Source concept:" : source cRud LINKTO INTERFACE "Concept"
  , "Target concept:" : target cRud LINKTO INTERFACE "Concept"
  , "Eigenschappen:" : propertyRule;declaredthrough cRud
  , "Properties:" : prop [Relation * Property] cRud
  , "Purpose:" : purpose[Relation*Purpose] cRud 
  , "Meaning:" : decMean[Relation*Meaning] cRud 
  ]

INTERFACE "Rules" FOR User, Student,Tutor : I[Rule] cRud BOX <SHCOLS>
  [ "Regel:" : I[Rule] cRud LINKTO INTERFACE "Rule"
  , "Formele expressie:" : formalExpression cRud
  , "Meaning:" : meaning[Rule*Meaning] cRud 
  , "Purpose:" : purpose[Rule*Purpose] cRud 
  ]

INTERFACE "Rule" FOR User, Student,Tutor : I[Rule] cRud BOX
  [ "Regel:" : I[Rule] cRud
  , "Formele expressie:" : formalExpression cRud
  , "Meaning:" : meaning[Rule*Meaning] cRud 
  , "Purpose:" : purpose[Rule*Purpose] cRud 
--  , "origin" : origin cRud  
  , "Conceptual diagram": (I[Rule]-I[PropertyRule]);conceptualDiagram cRud <ShowConceptualDiagram>
  ]

CLASSIFY PropertyRule ISA Rule

INTERFACE "Patterns" FOR User, Student,Tutor : I[Pattern] cRud BOX <SHCOLS>
  [ "Thema's:" : I cRud
  , "Purpose:" : purpose[Pattern*Purpose] cRud 
  ]

INTERFACE "Pattern" FOR User, Student,Tutor : I[Pattern] cRud BOX
  [ "Themes:" : I cRud
  , "Conceptual diagram": conceptualDiagram cRud <ShowConceptualDiagram>
  ]

VIEW ShowConceptualDiagram: FileObject { imgfile : filePath }
HTML TEMPLATE "ShowConceptualDiagram.html"
ENDVIEW

PURPOSE INTERFACE "Language"
{+This interface shows the business language from one PATTERN.
Its purpose is printed first to introduce the corresponding theme to the target audience.
It must correspond with chapter 2, the business language chapter, from the functional specifications as generated by the compiler.
+}
INTERFACE "Language" FOR User, Student, Tutor : I[Pattern] cRud BOX
  [ "Theme:" : I cRud
  , "Purpose:" : purpose[Pattern*Purpose] cRud
  , "Definitions:" : concepts[Pattern*Concept] cRud BOX
         [ "Concept" : name[Concept*ConceptName]
         , "Purpose" : purpose[Concept*Purpose] cRud
         ]
  ]

ENDCONTEXT




--  , "Gens" : context[Concept*Context]~ ;
--             (I[Concept]                                  /\
--                gengen[Gen*Concept]~;gengen[Gen*Concept]  /\
--              -(genspc[Gen*Concept]~;genspc[Gen*Concept])
--             )  
--  , "Roles" : allRoles[Context*Role]
--  , "allConjuncts" : allConjuncts[Context*Conjunct]
