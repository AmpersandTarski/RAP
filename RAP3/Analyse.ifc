CONTEXT RAP IN DUTCH

INTERFACE "Context" FOR User, Student,Tutor : I[Context] cRud BOX<ROWSNL>
      [ "Terug naar script" : context[ScriptVersion*Context]~;version~ cRud
      , "Gegevensmodel" : conceptualDiagram cRud <ShowConceptualDiagram>
      , ""     : I[Context] TABS
         [ "Concepten" : (context[Pattern*Context]~;concepts[Pattern*Concept]) \/ context[Concept*Context]~ cRud INTERFACE "Concepts"
         , "Relaties"  : declaredIn[Relation*Context]~  cRud INTERFACE "Relations"
         , "Regels"    : I
            TABS [ "gewone regels"          : allRules[Context*Rule] ;(I[Rule]-I[PropertyRule]) cRud INTERFACE "Rules"
                 , "multipliciteits regels" : allRules[Context*Rule] ;         I[PropertyRule];I[Rule]  cRud INTERFACE "Rules"
                 ]
         , "Patterns"   : context [Pattern * Context]~ cRud INTERFACE "Patterns"
         ]
  ]


INTERFACE "Concepts" FOR User, Student,Tutor : I[Concept] cRud BOX <SHCOLS>
  [ "Concepts:" : I cRud LINKTO INTERFACE "Concept"
  , "Purpose:" : purpose[Concept*Purpose];markupText cRud 
  ]

INTERFACE "Concept" FOR User, Student,Tutor : I[Concept] cRud BOX
  [ "Concept:" : I cRud
  , "Purpose:" : purpose[Concept*Purpose];markupText cRud 
  , "used in (relations):" : source~ \/ target~ cRud LINKTO INTERFACE "Relation"
  , "More generic concepts:" : genspc[Isa*Concept]~;gengen[Isa*Concept] \/ genspc[IsE*Concept]~;gengen[IsE*Concept] cRud LINKTO INTERFACE "Concept"
  , "More specific concepts:" :gengen[Isa*Concept]~;genspc[Isa*Concept] \/ gengen[IsE*Concept]~;genspc[IsE*Concept] cRud LINKTO INTERFACE "Concept"
  , "Conceptual diagram": conceptualDiagram cRud <ShowConceptualDiagram>
  ]

INTERFACE "Relations" FOR User, Student,Tutor : I[Relation] cRud BOX <SHCOLS>
  [ "Relation:" : I cRud -- name[Relation*RelationName] cRud en daar moet dan nog een view bij.
  , "Source concept:" : source cRud LINKTO INTERFACE "Concept"
  , "Target concept:" : target cRud LINKTO INTERFACE "Concept"
  , "Meaning:" : decmean[Relation*Meaning] cRud 
  ]

INTERFACE "Relation" FOR User, Student,Tutor : I[Relation] cRud BOX
  [ "Relation:" : I cRud LINKTO INTERFACE "Relation"
  , "Source concept:" : source cRud LINKTO INTERFACE "Concept"
  , "Target concept:" : target cRud LINKTO INTERFACE "Concept"
  , "Eigenschappen:" : propertyRule;declaredthrough cRud
  , "Properties:" : prop [Relation * Property] cRud
  , "Purpose:" : purpose[Relation*Purpose];markupText cRud 
  , "Meaning:" : decmean[Relation*Meaning] cRud 
  , "Regels met deze relatie:" : usedIn[Relation*Expression];formalExpression[Rule*Expression]~ cRud LINKTO INTERFACE "Rule"
  ]

INTERFACE "Rules" FOR User, Student,Tutor : I[Rule] cRud BOX <SHCOLS>
  [ "Regel:" : I[Rule] cRud LINKTO INTERFACE "Rule"
  , "Formele expressie:" : formalExpression cRud LINKTO INTERFACE "Expression"
  , "Meaning:" : meaning[Rule*Meaning] cRud 
  , "Purpose:" : purpose[Rule*Purpose];markupText cRud 
  , "Relaties in deze regel:" : formalExpression[Rule*Expression];usedIn[Relation*Expression]~ cRud LINKTO INTERFACE "Relation"
  ]

INTERFACE "Rule" FOR User, Student,Tutor : I[Rule] cRud BOX
  [ "Regel:" : I[Rule] cRud
  , "Formele expressie:" : formalExpression cRud
  , "Meaning:" : meaning[Rule*Meaning] cRud 
  , "Purpose:" : purpose[Rule*Purpose];markupText cRud 
--  , "origin" : origin cRud  
  , "Conceptual diagram": (I[Rule]-I[PropertyRule]);conceptualDiagram cRud <ShowConceptualDiagram>
  ]

INTERFACE "Expression" FOR User, Student,Tutor : I[Expression] cRud BOX
  [ "Expressie" : I[Expression] cRud
  , "Binaire Expressie: " : I[BinaryTerm] cRud COLS
           [ "links"    : first cRud
           , "operator" : operator cRud
           , "rechts"   : second cRud
           ]
  , "Unaire Expressie: " : I[UnaryTerm] cRud COLS
           [ "argument"    : arg cRud
           , "operator" : operator cRud
           ]
  , "Relatie: " : bind[BindedRelation*Relation] cRud LINKTO INTERFACE "Relation"
  , "SubExpressie van:" : first~ \/ second~ \/ arg~ cRud LINKTO INTERFACE "Expression"
  ]


INTERFACE "Patterns" FOR User, Student,Tutor : I[Pattern] cRud BOX <SHCOLS>
  [ "Thema's:" : I cRud
  , "Purpose:" : purpose[Pattern*Purpose];markupText cRud 
  ]

INTERFACE "Pattern" FOR User, Student,Tutor : I[Pattern] cRud BOX
  [ "Themes:" : I cRud
  , "Conceptual diagram": conceptualDiagram cRud <ShowConceptualDiagram>
  ]

VIEW ShowConceptualDiagram: FileObject { imgfile : filePath }
HTML TEMPLATE "ShowConceptualDiagram.html"
ENDVIEW

ENDCONTEXT




--  , "Gens" : context[Concept*Context]~ ;
--             (I[Concept]                                  /\
--                gengen[Gen*Concept]~;gengen[Gen*Concept]  /\
--              -(genspc[Gen*Concept]~;genspc[Gen*Concept])
--             )  
--  , "Roles" : allRoles[Context*Role]
--  , "allConjuncts" : allConjuncts[Context*Conjunct]
