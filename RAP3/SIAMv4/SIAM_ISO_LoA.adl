CONTEXT "Levels of Assurance" IN ENGLISH LATEX
PURPOSE CONTEXT "Levels of Assurance"
{+This file implements the basic relations and rules that are required for LoA's (Level of Assurance).
There are several kinds of LoA's, e.g.:
- ISO/IEC 29115:2013 (Entity authentication assurance framework).
- RFC 6711 (https://tools.ietf.org/html/rfc6711)
- NIST, "Electronic Authentication Guideline, NIST Special Publication 800-63-2", June 2004 (http://dx.doi.org/10.6028/NIST.SP.800-63-2.pdf)

This file provides a minimal functionality, using ISO LoA's, i.e. integers 1..5 (NIST LoAs are in the range 1..4).

Future work:
- replace the numbers by texts, e.g. `nist_auth_level_3` or `iso_auth_level_5`.
+}

CONCEPT LoA "a 'level of assurance' as specified in the ISO/IEC 29115:2013 standard"
REPRESENT LoA TYPE INTEGER

loaGTE :: LoA * LoA [RFX,ASY,TRN] -- SRC LoA is greater than or equal to TGT LoA
POPULATION loaGTE CONTAINS 
[ (1, 1)
, (2, 1), (2, 2)
, (3, 1), (3, 2), (3, 3)
, (4, 1), (4, 2), (4, 3), (4, 4)
, (5, 1), (5, 2), (5, 3), (5, 4), (5, 5)
]

RULE "Levels of Assurance (LoAs) must be in the range [1-5]": I[LoA] = 1 \/ 2 \/ 3 \/ 4 \/ 5 

sessionReqdLoA :: SESSION * LoA [UNI] -- sessionAccount must be authenticated with at least the required ISO level
sessionAuthLoA :: SESSION * LoA [UNI] -- sessionAccount has been authenticated using the specified ISO level

RULE "The user must be authenticated with at least the required LoA": 
  sessionAuthLoA~;sessionReqdLoA |- loaGTE

-- sessionAuthLevel1 :: SESSION * SESSION [PROP] -- This does not seem to be used or needed anywhere...
ROLE ExecEngine MAINTAINS "By default, a session authentication level is `1`"
RULE "By default, a session authentication level is `1`":
  sessionIsUser |- sessionAuthLoA;sessionAuthLoA~
VIOLATION (TXT "{EX} InsPair;sessionAuthLoA;SESSION;", SRC I, TXT ";LoA;1")

ENDCONTEXT