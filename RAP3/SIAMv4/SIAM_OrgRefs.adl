CONTEXT "Organization References" IN ENGLISH
{- The purpose of this file is to provide 'OrgRef's, 
i.e. textstrings that may be used to refer to an organization.
-}
-----------------------------------------------------------
PURPOSE RELATION accOrgRef 
{+A common need is to associate information objects to organizations.
An example is to associate a contract to a organization (customer).
While the SIAM_OrgReg module allows for the creation of Organization objects,
this is sometimes overkill. Many times, it suffices to have a simple text
that refers to some organization, the details of which are apparent from the
context in which the object is being used. 
For such purposes, we need a relation that allows us to simply refer to a organization.
+}
accOrgRef :: Account * OrgRef [UNI] -- a text that refers to the organization associated with the account.
MEANING "An Account may have a textstring that refers to the Organization to which the Account is related"

sessionOrgRef :: SESSION * OrgRef [UNI]
MEANING "In a SESSION, a textstring that refers to the Organization ('at the other side') may be known."
ROLE ExecEngine MAINTAINS "Determine the OrgRef in a session"
RULE "Determine the OrgRef in a session": sessionAccount;accOrgRef |- sessionOrgRef
VIOLATION (TXT "{EX} InsPair;sessionOrgRef;SESSION;", SRC I, TXT ";OrgRef;", TGT I)
ROLE ExecEngine MAINTAINS "Clear the OrgRef in a session"
RULE "Clear the OrgRef in a session": sessionIsUser;sessionOrgRef |- sessionAccount;accOrgRef
VIOLATION (TXT "{EX} DelPair;sessionOrgRef;SESSION;", SRC I, TXT ";OrgRef;", TGT I)

--[Glossary]--
CONCEPT OrgRef "text used to refer to a organization"
REPRESENT OrgRef TYPE ALPHANUMERIC

-----------------------------------------------------------
ENDCONTEXT