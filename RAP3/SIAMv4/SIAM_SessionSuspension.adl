CONTEXT "SessionSuspension" IN ENGLISH LATEX

--[Session deactivation/reactivation]----------------------

PURPOSE RELATION firstSessionAccount
{+In order to enable the suspension of sessions, the first sessionaccount must be remembered since it must be used to verify that the user that logs in to a suspended session in order to reactivate it, is the same as the user that did the original login.+}
firstSessionAccount :: SESSION * Account [UNI]
MEANING "The Account that is authenticated first in a session, must be remembered."

ROLE ExecEngine MAINTAINS "Registering the first authenticated session account"
RULE "Registering the first authenticated session account": 
  ("_SESSION"[SESSION] - (firstSessionAccount;firstSessionAccount~));sessionAccount |- firstSessionAccount
MEANING "Any account may be used to log into a session for the first time."
VIOLATION (TXT "{EX} InsPair;firstSessionAccount;SESSION;", SRC I, TXT ";Account;", TGT I)

PURPOSE RULE "Integrity of session accounts"
{+At most one user may log into any session so as to guarantee that it is unambiguously clear which account was used for doing things in the system.+}
RULE "Integrity of session accounts": 
   ("_SESSION"[SESSION] /\ firstSessionAccount;firstSessionAccount~);sessionAccount |- firstSessionAccount 
MEANING "At most one account may be activated in any session."
MESSAGE "The session has a known user; only the original user can login with (correct) credentials."
VIOLATION (TXT "User '", SRC firstSessionAccount;accUserid, TXT "' was logged in.")

ENDCONTEXT