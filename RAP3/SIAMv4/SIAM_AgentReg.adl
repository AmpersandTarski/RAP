CONTEXT "Agent Registration" IN ENGLISH
{- This extension to `SIAM_AgentRefs.adl` implements an Agent registration in a relatively minimalistic fashion.
The purpose of this file is to provide a set of 'standard' relations that are commonly re-used in different projects.
Note that it does not contain an IDENT statement, so as to allow for easy extensions of the `Agent` concept.
Also note that relations are UNI, not TOT - this is to allow as much flexibility for re-use as possible.
-}
-----------------------------------------------------------
INCLUDE "./SIAM_AgentRefs.adl"

CLASSIFY Agent ISA Actor -- Integration with Persona

RELATION agentRef[Agent*AgentRef] [UNI] -- Note that this relation does NOT (always) identify an Agent!
MEANING "A textstring that represents (but not necessarily identifies) this Agent"

--[AgentAccounts and Sessions]--
agaccAgent :: AgentAccount * Agent [UNI] 
MEANING "An AgentAccount may contain (web)service-specific attributes regarding a specific Agent"

sessionAgent :: SESSION * Agent [UNI]
MEANING "In a SESSION, the Agent (i.e. the Agent that acts 'at the other side') may be known."
ROLE ExecEngine MAINTAINS "Determine the acting Agent in a session"
RULE "Determine the acting Agent in a session": sessionAgentAccount;agaccAgent |- sessionAgent
VIOLATION (TXT "{EX} InsPair;sessionAgent;SESSION;", SRC I, TXT ";Agent;", TGT I)
ROLE ExecEngine MAINTAINS "Clear the acting Agent in a session"
RULE "Clear the acting Agent in a session": sessionAgent |- sessionAgentAccount;agaccAgent
VIOLATION (TXT "{EX} DelPair;sessionAgent;SESSION;", SRC I, TXT ";Agent;", TGT I)

--[Integration with SIAM_AgentRef]--

ROLE ExecEngine MAINTAINS "Auto maintain 'agaccAgentRef' relation"
RULE "Auto maintain 'agaccAgentRef' relation": agaccAgent;agentRef |- agaccAgentRef
VIOLATION (TXT "{EX} InsPair;agaccAgentRef;AgentAccount;", SRC I, TXT ";AgentRef;", TGT I)

-----------------------------------------------------------
ENDCONTEXT