CONTEXT RAP3 IN ENGLISH

INTERFACE MyScripts FOR Tutor,Student: "_SESSION";sessionAccount;submittor~ cRuD BOX <SHCOLS>
-- Notice that the cRuD on line 3 must not be CRuD, because the green '+' generates a Slim error at run time. See issue #704.
    [ content  : I cRud
    , opdracht : assignment
    , tijdstip : submitted cRud
    ]

INTERFACE StudentScripts FOR Tutor : "_SESSION"[SESSION]#(I[Account]/\submittor~;submittor) cRud BOX <SCOLS>
    [ indiener : I cRud
    , scripts : submittor~ cRud BOX<SCOLS>  -- never empty, because of precondition I[Account]/\submittor~;submittor
       [ opdracht  : assignment cRud
       , content   : I cRud
       , ingediend : submitted cRud
       ]
    ]

INTERFACE "Nieuw script" FOR Student, User : I[Script] CRUd BOX <ROWSNL>
   [ "Meta-info": I cRud BOX <SCOLS>
      [ indiener  : submittor cRud
      , opdracht  : assignment CRUd
      , ingediend : submitted cRud
      ]
   , "Content":  content cRUd
   , "Actual info": I cRud ROWS
      [ compile : I BOX <PropertyButton>
                  [ disabled : I-I            cRud
                  , property : compileRequest cRUd
                  ]
      , "Compiler message"  : compileresponse cRud
      , controls: seqFirstItem ; I[ScriptVersion] cRud COLS
         [ "Diagnosis" :  I BOX <PropertyButton>
                           [ disabled : I/\diag;diag~          cRud
                           , property : diagRequest cRUd
                           ]
         , "Func. spec + pictures" : I BOX <PropertyButton>
                           [ disabled : I/\funcSpec;funcSpec~  cRud
                           , property : funcSpecRequest cRUd
                           ]
-- Atlas kan erg lang duren. Wellicht in combinatie met het niet goed werken van multiuser functionaliteit? Uitzoeken! HJO, 20170423
--         , "Atlas" :      I BOX <PropertyButton>
--                           [ disabled : (I/\loadedInRAP3Ok)\/(I-funcSpec;funcSpec~) cRud -- Only active if there are graphics to display.
--                           , property : loadRequest cRUd
--                           ]
         , "Prototype" :  I BOX <PropertyButton>
                           [ disabled : I/\protoOk        cRud
                           , property : protoRequest cRUd
                           ]
         ]
      , "links to your results": seqFirstItem ; I[ScriptVersion] cRud BOX <COLSNL>
         [ "Diagnosis"  : diag cRud
         , "Func. spec" : funcSpec cRud
         , "Atlas" :      context cRud LINKTO INTERFACE "Context"
         , "Prototype" :  protoOk;version~;proto cRud
         ]
      ]
   ]

INTERFACE "ScriptContent" FOR SYSTEM : I[Script];content BOX []

ENDCONTEXT