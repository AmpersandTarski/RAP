CONTEXT "ISO Authentication levels" IN ENGLISH LATEX
PURPOSE CONTEXT "ISO Authentication levels"
{+This file implements the basic relations and rules that are required for
LoA's (Level of Assurance), according to the specifications thereof in 
ISO/IEC 29115:2013 (Entity authentication assurance framework).
+}

CONCEPT ISOLevel "a 'level of assurance' as specified in the ISO/IEC 29115:2013 standard"
REPRESENT ISOLevel TYPE INTEGER

RELATION isoLevelGTE[ISOLevel*ISOLevel] [RFX,ASY,TRN] -- SRC ISOLevel is greater than or equal to TGT ISOLevel
POPULATION isoLevelGTE CONTAINS 
[ (1, 1)
, (2, 1), (2, 2)
, (3, 1), (3, 2), (3, 3)
, (4, 1), (4, 2), (4, 3), (4, 4)
, (5, 1), (5, 2), (5, 3), (5, 4), (5, 5)
]

RULE "ISOLevels (LoAs) must be in the range [1-5]": I[ISOLevel] = '1' \/ '2' \/ '3' \/ '4' \/ '5' 

RELATION sessionReqdISOLevel[SESSION*ISOLevel] [UNI] -- sessionAccount must be authenticated with at least the required ISO level
RELATION sessionAuthISOLevel[SESSION*ISOLevel] [UNI] -- sessionAccount has been authenticated using the specified ISO level

RULE "The user must be authenticated with at least the required ISOLevel": 
  sessionAuthISOLevel~;sessionReqdISOLevel |- isoLevelGTE

-- RELATION sessionAuthLevel1[SESSION*SESSION] [PROP] -- This does not seem to be used or needed anywhere...
ROLE ExecEngine MAINTAINS "By default, a session authentication level is `1`"
RULE "By default, a session authentication level is `1`":
  I[SESSION] /\ sessionAccount;sessionAccount~ |- sessionAuthISOLevel;sessionAuthISOLevel~
VIOLATION (TXT "{EX} InsPair;sessionAuthISOLevel;SESSION;", SRC I, TXT ";ISOLevel;1")

ENDCONTEXT