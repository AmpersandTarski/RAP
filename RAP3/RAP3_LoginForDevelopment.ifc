CONTEXT "LoginWithAssistance" IN ENGLISH LATEX
{-This script is intended to be reusable, and for INCLUDEsion in your own scripts.
This interface provides you with LOGIN functionality.
It is particularly suited for developers, since it allows you to select an account
by which you then will be logged in, rather than that you have to type the actual
username and password.
-}
-----------------------------------------------------------

INTERFACE "Login (DEV)": "_SESSION"[SESSION] cRud BOX <ROWSNL>
   [ "Login" : (I-sessionAccount;sessionAccount~)-regCreateAccount cRud BOX <HCOLS>
      [ "Userid": loginUserid cRUd
      , "Password": loginPassword crUd -- crUd is needed for Passwords
      , "Please help": sessionLoginAssist cRUd --$This line, and the next, are meant for demonstration contexts only
      , " ": sessionLoginAssist;(V-V;I[Account];V);'Cannot help - there are no accounts'[LoginMessage] cRud
      ]
--$This is for an easy login, which is just for testing purposes
   , "LoginAssist ": sessionLoginAssist;V[SESSION*Account] cRud BOX <SHCOLS>
      [ "Login?": autoLoginAccount cRUd
      , "Userid": accUserid cRud
      , "Organization": accOrg cRud
      , "Person": accPerson cRud
--    , "UIDs": accUID cRud
--    , "Allowed roles": accAllowedRoles cRUd
--    , "Default roles": accDefaultRoles cRUd
      ]
   , "Registration": (I-sessionAccount;sessionAccount~)-sessionLoginAssist cRud BOX <HCOLS>
      [ "Registreren?" : regCreateAccount cRUd
      , "Studentnummer" : regUserID cRUd
      , "Password" : regPassword1 crUd -- crUd is needed for Passwords
      , "(retype)" : regPassword2 crUd -- crUd is needed for Passwords
      , "OU Email" : regEmail cRUd
      ]
   , "Logout": I /\ sessionAccount;sessionAccount~ cRud BOX <ROWSNL>
      [ "Logout": I cRud BOX <HCOLS>
         [ "Logout?": logoutRequest cRUd
         , "UserID": sessionUserid cRud
--       , "Organization": accOrg cRud
--       , "Person": accPerson cRud
         , "Email": sessionAccount;accEmail cRud
--       , "Allowed roles": sessionAllowedRoles cRUd
--       , "Active roles": sessionActiveRoles cRUd
         ]
      ]
   ]
POPULATION LoginMessage CONTAINS [ "Cannot help - there are no accounts" ]

--[Assisted Login rules]-----------------------------------

sessionLoginAssist :: SESSION * SESSION [PROP]
ROLE ExecEngine MAINTAINS "Reset login help"
RULE "Reset login help": sessionLoginAssist /\ sessionAccount;sessionAccount~ |- -V
VIOLATION (TXT "{EX} DelPair;sessionLoginAssist;SESSION;", SRC I, TXT ";SESSION;", TGT I)

-----------------------------------------------------------
ENDCONTEXT